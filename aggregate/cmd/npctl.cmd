@REM author: wzdnzd
@REM date: 2022-08-24
@REM describe: network proxy controller for clash

@echo off & PUSHD %~DP0 & cd /d "%~dp0"

@REM change encoding
chcp 65001 >nul 2>nul

@REM https://blog.csdn.net/sanqima/article/details/37818115
setlocal enableDelayedExpansion

@REM output with color
call :setESC

@REM call workflow
goto :workflow


@REM ########################
@REM function define blow ###
@REM ########################

@REM process pipeline
:workflow
@REM batch file name
set "batname=%~nx0"

@REM microsoft terminal displays differently from cmd and powershell
@REM call :ismsterminal msterminal
set "msterminal=1"

@REM enable create shortcut 
set "enableshortcut=1"

@REM enable download config from remote
set "enableremoteconf=1"
set "remoteurl="

@REM validate configuration files before starting
set "verifyconf=0"

@REM check and update wintun.dll
set "checkwintun=0"

@REM info color
set "infocolor=92"
set "warncolor=93"

if "!msterminal!" == "1" (
    set "infocolor=95"
    set "warncolor=97"
)

@REM print heart
set "customize=1"
set "drawheart=0"

@REM exit flag
set "shouldexit=0"

@REM init
set "initflag=0"

@REM configuration file name
set "configuration=config.yaml"

@REM subscription link
set "sublink="
set "isweblink=0"

@REM check
set "testflag=0"

@REM repair
set "repair=0"

@REM only reload
set "reloadonly=0"

@REM restart clash.exe
set "restartflag=0"

@REM close proxy
set "killflag=0"

@REM update
set "updateflag=0"

@REM purge
set "purgeflag=0"

@REM only update subscriptions and rulesets
set "quickflag=0"

@REM don't update subscription
set "exclude=0"

@REM use clash.meta
set "clashmeta=0"

@REM use clash.premium
set "clashpremium=0"

@REM alpha version allowed
set "alpha=0"

@REM yacd dashboard, see https://github.com/MetaCubeX/Yacd-meta
set "yacd=0"

@REM run on background
set "asdaemon=0"

@REM show window
set "show=0"

@REM setting workspace
set "dest="

@REM network proxy registry configuration path
set "proxyregpath=HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"

@REM autostart registry configuration path
set "autostartregpath=HKCU\Software\Microsoft\Windows\CurrentVersion\Run"
set "startupapproved=HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\StartupApproved\Run"

@REM parse arguments
call :argsparse %*

@REM invalid arguments
if "!shouldexit!" == "1" exit /b 1

@REM regular file path
if "!dest!" == "" set "dest=%~dp0"
call :pathregular dest "!dest!"

@REM auto start vb script
set "startupvbs=!dest!\startup.vbs"

@REM auto update vb script
set "updatevbs=!dest!\update.vbs"

@REM draw a heart
if "!drawheart!"== "1" goto :printheart

@REM close network proxy
if "!killflag!" == "1" goto :closeproxy

@REM clean all setting
if "!purgeflag!" == "1" goto :purge

@REM prevent precheck if no action
if "!reloadonly!" == "0" if "!restartflag!" == "0" if "!repair!" == "0" if "!testflag!" == "0" if "!updateflag!" == "0" if "!initflag!" == "0" (
    @REM @echo [%ESC%[91mé”™è¯¯%ESC%[0m] å¿…é¡»åŒ…å« [%ESC%[!warncolor!m-f%ESC%[0m %ESC%[!warncolor!m-i%ESC%[0m %ESC%[!warncolor!m-k%ESC%[0m %ESC%[!warncolor!m-r%ESC%[0m %ESC%[!warncolor!m-t%ESC%[0m %ESC%[!warncolor!m-u%ESC%[0m] ä¸­çš„ä¸€ç§æ“ä½œ
    @REM @echo.

    if "!shouldexit!" == "0" goto :usage
    exit /b
)

@REM config file path
call :precheck configfile
if "!configfile!" == "" exit /b 1

@REM connectivity test
if "!testflag!" == "1" (
    call :checkconnect available 1
    exit /b
)

@REM reload config
if "!reloadonly!" == "1" goto :reload

@REM update
if "!restartflag!" == "1" goto :restartprogram

@REM check issues
if "!repair!" == "1" goto :resolveissues

@REM update
if "!updateflag!" == "1" goto :updateplugins

@REM init
if "!initflag!" == "1" goto :initialize

@REM unknown command
@REM if "!shouldexit!" == "0" goto :usage

exit /b


@REM check if the configuration file exists
:precheck <result>
set "%~1="
set "subfile=!temp!\clashsub.yaml"

@REM absolute path
call :pathconvert conflocation "!configuration!"
call :pathregular conflocation "!conflocation!"

if "!conflocation!" == "" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] é…ç½®æ–‡ä»¶è·¯å¾„%ESC%[91mæ— æ•ˆ%ESC%[0m
    exit /b 1
)

@REM cannot contain whitespace in path
if "!conflocation!" NEQ "!conflocation: =!" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ•ˆçš„é…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!conflocation!%ESC%[0m"ï¼Œ è·¯å¾„ä¸èƒ½åŒ…å«%ESC%[!warncolor!mç©ºæ ¼%ESC%[0m
    exit /b 1
)

if "!isweblink!" == "1" (
    if exist "!conflocation!" (
        set "tips=[%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] %ESC%[!warncolor!må·²å­˜åœ¨%ESC%[0mé…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!conflocation!%ESC%[0m" ä¼šè¢«%ESC%[91mè¦†ç›–%ESC%[0mï¼Œæ˜¯å¦ç»§ç»­ï¼Ÿ (%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
        if "!msterminal!" == "1" (
            choice /t 6 /d n /n /m "!tips!"
        ) else (
            set /p "=!tips!" <nul
            choice /t 6 /d n /n
        )
        if !errorlevel! == 2 exit /b 1
    )

    @REM try to download
    del /f /q "!subfile!" >nul 2>nul

    set "statuscode=000"
    for /f %%a in ('curl --retry 3 --retry-max-time 30 -m 60 --connect-timeout 30 -L -s -o "!subfile!" -w "%%{http_code}" -H "User-Agent: Clash" "!sublink!"') do set "statuscode=%%a"

    @REM download success
    if "!statuscode!" == "200" (
        set "filesize=0"
        if exist "!subfile!" (for %%a in ("!subfile!") do set "filesize=%%~za")
        if !filesize! GTR 64 (
            @REM validate
            set "content="
            for /f "tokens=*" %%a in ('findstr /i /r /c:"^external-controller:[ ][ ]*.*:[0-9][0-9]*.*" !subfile!') do set "content=%%a"
            if "!content!" == "" (
                @echo [%ESC%[91mé”™è¯¯%ESC%[0m] è®¢é˜… "%ESC%[!warncolor!m!sublink!%ESC%[0m" æ— æ•ˆï¼Œè¯·æ£€æŸ¥ç¡®è®¤
                exit /b 1
            )

            del /f /q "!conflocation!" >nul 2>nul
            call :splitpath filepath filename "!conflocation!"
            call :makedirs success "!filepath!"
            if "!success!" == "0" (
                @echo [%ESC%[91mé”™è¯¯%ESC%[0m] åˆ›å»ºæ–‡ä»¶å¤¹ "%ESC%[!warncolor!m!filepath!%ESC%[0m" %ESC%[91må¤±è´¥%ESC%[0mï¼Œè¯·ç¡®è®¤è·¯å¾„æ˜¯å¦åˆæ³• 
                exit /b 1
            )

            move "!subfile!" "!conflocation!" >nul 2>nul
            @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] è®¢é˜…ä¸‹è½½%ESC%[!infocolor!mæˆåŠŸ%ESC%[0m

            @REM ä¿å­˜è®¢é˜…é“¾æ¥
            @echo !sublink! > "!filepath!\subscriptions.txt"
        ) else (
            @REM output is empty
            set "statuscode=000"
        )
    )

    if "!statuscode!" NEQ "200" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] è®¢é˜…ä¸‹è½½%ESC%[91må¤±è´¥%ESC%[0mï¼Œ è¯·æ£€æŸ¥ç¡®è®¤æ­¤è®¢é˜…æ˜¯å¦æœ‰æ•ˆ
        exit /b 1
    )
)

if not exist "!conflocation!" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] é…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!conflocation!%ESC%[0m" %ESC%[91mä¸å­˜åœ¨%ESC%[0m
    goto :eof
)

@REM validate
set "content="
for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"^proxy-groups:[ ]*" "!conflocation!"') do set "content=%%a"
call :trim content "!content!"
if "!content!" NEQ "proxy-groups" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] %ESC%[91mæ— æ•ˆ%ESC%[0mçš„é…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!conflocation!%ESC%[0m"
    exit /b 1
)

set "%~1=!conflocation!"
goto :eof


@REM Initialize network proxy
:initialize
set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] ç½‘ç»œä»£ç†ç¨‹åºå°†åœ¨ç›®å½• "%ESC%[!warncolor!m!dest!%ESC%[0m" å®‰è£…å¹¶è¿è¡Œï¼Œæ˜¯å¦ç»§ç»­ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
if "!msterminal!" == "1" (
    choice /t 5 /d n /n /m "!tips!"
) else (
    set /p "=!tips!" <nul
    choice /t 5 /d n /n
)
if !errorlevel! == 2 exit /b 1

set "quickflag=0"
set "exclude=1"
call :updateplugins
goto :eof


@REM fix network issues
:resolveissues
@REM mandatory use of the stable version
set "alpha=0"

@echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹æ£€æŸ¥å¹¶å°è¯•ä¿®å¤ç½‘ç»œä»£ç†ï¼Œè¯·ç¨ç­‰

@REM check status
call :checkconnect available 0
set "lazycheck=0"
if "!available!" == "1" (
    set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] ä»£ç†ç½‘ç»œè¿è¡Œ%ESC%[!infocolor!mæ­£å¸¸%ESC%[0mï¼Œ%ESC%[91mä¸å­˜åœ¨%ESC%[0mé—®é¢˜ï¼Œæ˜¯å¦ç»§ç»­ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
    if "!msterminal!" == "1" (
        choice /t 5 /d n /n /m "!tips!"
    ) else (
        set /p "=!tips!" <nul
        choice /t 5 /d n /n
    )
    if !errorlevel! == 2 exit /b 1
) else (
    @REM running detect
    call :isrunning status
    if "!status!" == "0" (
        call :checkwapper continue 1
        if "!continue!" == "0" exit /b
    ) else set "lazycheck=1"
)

@REM O: Reload | R: Restart | U: Restore | N: Cancel
set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] æŒ‰ %ESC%[!warncolor!mO%ESC%[0m %ESC%[!warncolor!mé‡è½½%ESC%[0mï¼ŒæŒ‰ %ESC%[!warncolor!mR%ESC%[0m %ESC%[!warncolor!mé‡å¯%ESC%[0mï¼ŒæŒ‰ %ESC%[!warncolor!mU%ESC%[0m %ESC%[!warncolor!mæ¢å¤%ESC%[0mè‡³é»˜è®¤ï¼ŒæŒ‰ %ESC%[!warncolor!mN%ESC%[0m %ESC%[!warncolor!må–æ¶ˆ%ESC%[0m (%ESC%[!warncolor!mO%ESC%[0m/%ESC%[!warncolor!mR%ESC%[0m/%ESC%[!warncolor!mU%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
if "!msterminal!" == "1" (
    choice /t 6 /c ORUN /d R /n /m "!tips!"
) else (
    set /p "=!tips!" <nul
    choice /t 6 /c ORUN /d R /n
)

if !errorlevel! == 1 (
    call :reload
) else if !errorlevel! == 2 (
    call :restartprogram
) else if !errorlevel! == 3 (
    @REM kill clash process
    call :killprocesswrapper

    @REM lazy check
    if "!lazycheck!" == "1" (
        call :checkwapper continue 0
        if "!continue!" == "0" exit /b
    )

    @REM restore plugins
    call :updateplugins
) else (
    :: cancel
    exit /b
)

for /l %%i in (1,1,5) do (
    @REM recheck
    call :checkconnect available 0
    if "!available!" == "1" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] é—®é¢˜ä¿®å¤%ESC%[!infocolor!mæˆåŠŸ%ESC%[0mï¼Œç½‘ç»œä»£ç†å¯%ESC%[!infocolor!mæ­£å¸¸%ESC%[0mä½¿ç”¨
        exit /b
    ) else (
        @REM wait
        timeout /t 1 /nobreak >nul 2>nul
    )
)

@echo [%ESC%[91mé”™è¯¯%ESC%[0m] é—®é¢˜ä¿®å¤%ESC%[91må¤±è´¥%ESC%[0mï¼Œ ç½‘ç»œä»£ç†ä»%ESC%[91mæ— æ³•%ESC%[0mä½¿ç”¨ï¼Œ è¯·å°è¯•å…¶ä»–æ–¹æ³•
goto :eof


@REM check if the network is available
:checkwapper <result> <enable>
set "%~1=1"
call :trim loglevel "%~2"
if "!loglevel!" == "" set "loglevel=1"

call :isavailable available 0 "https://www.baidu.com" ""
if "!available!" == "0" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œ%ESC%[91mä¸å¯ç”¨%ESC%[0mï¼Œ ä½†ä»£ç†ç¨‹åº%ESC%[91må¹¶æœªè¿è¡Œ%ESC%[0mï¼Œè¯·æ£€æŸ¥ä½ çš„%ESC%[!warncolor!mæœ¬åœ°ç½‘ç»œ%ESC%[0mæ˜¯å¦æ­£å¸¸

    @REM should terminate
    set "%~1=0"
    exit /b
)

if "!loglevel!" == "1" (
    @echo [%ESC%[!warncolor!mæç¤º%ESC%[0m] ç½‘ç»œä»£ç†%ESC%[91mæ²¡æœ‰å¼€å¯%ESC%[0mï¼Œ æ¨èé€‰æ‹© %ESC%[!warncolor!mRestart%ESC%[0m å¼€å¯
)
goto :eof


@REM update workflow
:updateplugins
set "downloaded=0"

if "!quickflag!" == "1" (
    call :quickupdate modified
    if "!modified!" == "0" (exit /b 0) else (set "downloaded=1")
)

@REM run as admin
if "!asdaemon!" == "1" (
    cacls "%SystemDrive%\System Volume Information" >nul 2>&1 || (start "" mshta vbscript:CreateObject^("Shell.Application"^).ShellExecute^("%~snx0"," %*","","runas",!show!^)^(window.close^)&exit /b)
)

@REM prepare all plugins
call :prepare changed 1 !downloaded!

@REM no new version found
if "!changed!" == "0" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å½“å‰å·²æ˜¯æœ€æ–°ç‰ˆæœ¬ï¼Œæ— éœ€æ›´æ–°
) else (
    @REM wait for overwrite files
    timeout /t 1 /nobreak >nul 2>nul
)

@REM postclean
call :cleanworkspace "!temp!"

@REM startup
call :startclash
goto :eof


@REM parse and validate arguments
:argsparse
set result=false

if "%1" == "-a" set result=true
if "%1" == "--alpha" set result=true
if "!result!" == "true" (
    set "alpha=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-c" set result=true
if "%1" == "--conf" set result=true
if "!result!" == "true" (
    @REM validate argument
    call :trim subscription "%~2"

    if "!subscription!" == "" set result=false
    if "!subscription:~0,2!" == "--" set result=false
    if "!subscription:~0,1!" == "-" set result=false

    if "!result!" == "false" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] å¦‚æœæŒ‡å®šå‚æ•° "%ESC%[!warncolor!m--conf%ESC%[0m" æˆ–è€… "%ESC%[!warncolor!m--c%ESC%[0m" åˆ™å¿…é¡»æä¾›æœ‰æ•ˆçš„%ESC%[!warncolor!mé…ç½®æ–‡ä»¶%ESC%[0mæˆ–%ESC%[!warncolor!mè®¢é˜…%ESC%[0m
        @echo.
        goto :usage
    )

    if "!subscription:~0,8!" == "https://" set "isweblink=1"
    if "!subscription:~0,7!" == "http://" set "isweblink=1"
    if "!isweblink!" == "1" (
        set "invalid=0"

        @REM include '"' see https://stackoverflow.com/questions/46238709/how-to-detect-if-input-is-quote
        @echo !subscription! | findstr /i /r /c:"\"^" >nul && (set "invalid=1")

        @REM contain whitespace
        if "!subscription!" neq "!subscription: =!" set "invalid=1"
        @REM match url
        echo "!subscription!" | findstr /i /r /c:^"\"http.*://.*[a-zA-Z0-9][a-zA-Z0-9]*\"^" >nul 2>nul || (set "invalid=1")

        if "!invalid!" == "1" (
            set "shouldexit=1"

            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ•ˆçš„è®¢é˜…é“¾æ¥ "%ESC%[!warncolor!m!subscription!%ESC%[0m"
            @echo.
            goto :eof
        ) 
        set "sublink=!subscription!"
    ) else (
        set "invalid=1"
        if "!subscription:~-5!" == ".yaml" (set "invalid=0") else (
            if "!subscription:~-4!" == ".yml" (set "invalid=0")
        )
        if "!invalid!" == "0" (
            set "configuration=!subscription!"
        ) else (
            set "shouldexit=1"

            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ•ˆçš„é…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!subscription!%ESC%[0m"ï¼Œä»…æ”¯æŒ "%ESC%[!warncolor!m.yaml%ESC%[0m" å’Œ "%ESC%[!warncolor!m.yml%ESC%[0m" æ ¼å¼
            @echo.
            goto :eof
        )
    )
    shift & shift & goto :argsparse
)

if "%1" == "-d" set result=true
if "%1" == "--daemon" set result=true
if "!result!" == "true" (
    set "asdaemon=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-e" set result=true
if "%1" == "--exclude" set result=true
if "!result!" == "true" (
    set "exclude=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-f" set result=true
if "%1" == "--fix" set result=true
if "!result!" == "true" (
    set "repair=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-h" set result=true
if "%1" == "--help" set result=true
if "!result!" == "true" (
    call :usage
)

if "%1" == "-i" set result=true
if "%1" == "--init" set result=true
if "!result!" == "true" (
    set "initflag=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-k" set result=true
if "%1" == "--kill" set result=true
if "!result!" == "true" (
    set "killflag=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-l" set result=true
if "%1" == "--love" set result=true
if "!result!" == "true" (
    if "!customize!" == "1" (
        set "drawheart=1"
        set result=false
        shift & goto :argsparse
    ) else (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æœªçŸ¥å‚æ•°ï¼š%ESC%[91m%1%ESC%[0m
        @echo.
        goto :usage
    )
)

if "%1" == "-m" set result=true
if "%1" == "--meta" set result=true
if "!result!" == "true" (
    set "clashmeta=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-n" set result=true
if "%1" == "--native" set result=true
if "!result!" == "true" (
    set "clashpremium=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-o" set result=true
if "%1" == "--overload" set result=true
if "!result!" == "true" (
    set "reloadonly=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-p" set result=true
if "%1" == "--purge" set result=true
if "!result!" == "true" (
    set "purgeflag=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-q" set result=true
if "%1" == "--quick" set result=true
if "!result!" == "true" (
    set "quickflag=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-r" set result=true
if "%1" == "--restart" set result=true
if "!result!" == "true" (
    set "restartflag=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-s" set result=true
if "%1" == "--show" set result=true
if "!result!" == "true" (
    set "show=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-t" set result=true
if "%1" == "--test" set result=true
if "!result!" == "true" (
    set "testflag=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-u" set result=true
if "%1" == "--update" set result=true
if "!result!" == "true" (
    set "updateflag=1"
    set result=false
    shift & goto :argsparse
)

if "%1" == "-w" set result=true
if "%1" == "--workspace" set result=true
if "!result!" == "true" (
    @REM validate argument
    call :trim param "%~2"
    if "!param!" == "" set result=false
    if "!param:~0,2!" == "--" set result=false
    if "!param:~0,1!" == "-" set result=false

    if "!result!" == "false" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ•ˆçš„å‚æ•°ï¼Œå¦‚æœæŒ‡å®š "%ESC%[!warncolor!m--workspace%ESC%[0m"ï¼Œ"%ESC%[!warncolor!m!param!%ESC%[0m"ï¼Œåˆ™éœ€æä¾›æœ‰æ•ˆçš„è·¯å¾„
        @echo.
        goto :usage
    )

    call :pathconvert directory "!param!"
    if not exist "!directory!" (
        call :makedirs success "!directory!"
        if "!success!" == "1" (rd "!directory!" /s /q >nul 2>nul) else (set "shouldexit=1")
    )

    if "!shouldexit!" == "1" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] å‚æ•° "%ESC%[!warncolor!m--workspace%ESC%[0m" æŒ‡å®šçš„æ–‡ä»¶å¤¹è·¯å¾„ "%ESC%[!warncolor!m!directory!%ESC%[0m" %ESC%[91mæ— æ•ˆ%ESC%[0m
        @echo.
        goto :eof
    )

    set "dest=!directory!"
    set result=false
    shift & shift & goto :argsparse
)

if "%1" == "-y" set result=true
if "%1" == "--yacd" set result=true
if "!result!" == "true" (
    set "yacd=1"
    set result=false
    shift & goto :argsparse
)

@REM will throw exception if this code not in here or delete it or merge with <if "%1" NEQ "">. why?
if "%1" == "" goto :eof

if "%1" NEQ "" (
    call :trim syntax "%~1"
    if "!syntax!" == "goto" (
        call :trim funcname "%~2"
        if "!funcname!" == "" (
            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ•ˆçš„è¯­æ³•ï¼Œè°ƒç”¨ "%ESC%[!warncolor!mgoto%ESC%[0m" æ—¶å¿…é¡»æä¾›å‡½æ•°å
            goto :usage
        )

        for /f "tokens=1-2,* delims= " %%a in ("%*") do set "params=%%c"
        if "!params!" == "" (
            call !funcname!
            exit /b
        ) else (
            call !funcname! !params!
            exit /b
        )
    )

    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æœªçŸ¥å‚æ•°ï¼š%ESC%[91m%1%ESC%[0m
    @echo.
    goto :usage
)

goto :eof


@REM help
:usage
@echo ä½¿ç”¨æ–¹æ³•ï¼š!batname! [%ESC%[!warncolor!måŠŸèƒ½é€‰é¡¹%ESC%[0m] [%ESC%[!warncolor!må…¶ä»–å‚æ•°%ESC%[0m]ï¼Œæ”¯æŒ %ESC%[!warncolor!m-%ESC%[0m å’Œ %ESC%[!warncolor!m--%ESC%[0m ä¸¤ç§æ¨¡å¼
@echo.
@echo åŠŸèƒ½é€‰é¡¹ï¼š
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -f, --fix             æ£€æŸ¥å¹¶å°è¯•ä¿®å¤ä»£ç†ç½‘ç»œ
@echo -h, --help            æ‰“å°å¸®åŠ©ä¿¡æ¯
@echo -i, --init            åˆ©ç”¨ %ESC%[!warncolor!m--conf%ESC%[0m æä¾›çš„é…ç½®æ–‡ä»¶åˆ›å»ºä»£ç†ç½‘ç»œ
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -k, --kill            é€€å‡ºç½‘ç»œä»£ç†ç¨‹åº
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
if "!customize!" == "1" (
    @echo -l, --love            å½“ç„¶æ˜¯å¤§å£°å‘Šè¯‰æˆ‘å®æˆ‘çˆ±å¥¹å•¦ğŸ¤ªğŸ¤ªğŸ¤ª
)
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -o, --overload        é‡æ–°åŠ è½½é…ç½®æ–‡ä»¶
@echo -p, --purge           å…³é—­ç³»ç»Ÿä»£ç†å¹¶ç¦æ­¢ç¨‹åºå¼€æœºè‡ªå¯ï¼Œå–æ¶ˆè‡ªåŠ¨æ›´æ–°
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -r, --restart         é‡å¯ç½‘ç»œä»£ç†ç¨‹åº
@echo -t, --test            æµ‹è¯•ä»£ç†ç½‘ç»œæ˜¯å¦å¯ç”¨
@echo -u, --update          æ›´æœ‰æ‰€æœ‰ç»„ä»¶ï¼ŒåŒ…æ‹¬ clash.exeã€è®¢é˜…ã€ä»£ç†è§„åˆ™ä»¥åŠ IP åœ°å€æ•°æ®åº“ç­‰
echo.
@echo å…¶ä»–å‚æ•°ï¼š
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -a, --alpha           æ˜¯å¦å…è®¸ä½¿ç”¨é¢„è§ˆç‰ˆï¼Œé»˜è®¤ä¸ºç¨³å®šç‰ˆï¼Œæ­é… %ESC%[!warncolor!m-i%ESC%[0m æˆ–è€… %ESC%[!warncolor!m-u%ESC%[0m ä½¿ç”¨
@echo -c, --conf            é…ç½®æ–‡ä»¶ï¼Œæ”¯æŒæœ¬åœ°é…ç½®æ–‡ä»¶å’Œè®¢é˜…é“¾æ¥ï¼Œé»˜è®¤ä¸ºå½“å‰ç›®å½•ä¸‹çš„ %ESC%[!warncolor!mconfig.yaml%ESC%[0m
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -d, --daemon          åå°é™é»˜æ‰§è¡Œï¼Œç¦æ­¢æ‰“å°æ—¥å¿—
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -e, --exclude         æ›´æ–°æ—¶è·³è¿‡ä»£ç†é›†ä¸­é…ç½®çš„è®¢é˜…
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -m, --meta            å¦‚æœé…ç½®å…¼å®¹ï¼Œä½¿ç”¨ clash.meta ä»£æ›¿ clash.premiumï¼Œæ­é… %ESC%[!warncolor!m-i%ESC%[0m æˆ– %ESC%[!warncolor!m-u%ESC%[0m ä½¿ç”¨
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -n, --native          å¼ºåˆ¶ä½¿ç”¨ clash.premiumï¼Œæ­é… %ESC%[!warncolor!m-i%ESC%[0m æˆ– %ESC%[!warncolor!m-u%ESC%[0m ä½¿ç”¨
@echo -q, --quick           ä»…æ›´æ–°æ–°è®¢é˜…å’Œä»£ç†è§„åˆ™ï¼Œæ­é… %ESC%[!warncolor!m-u%ESC%[0m ä½¿ç”¨
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -s, --show            æ–°çª—å£ä¸­æ‰§è¡Œï¼Œé»˜è®¤ä¸ºå½“å‰çª—å£
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -w, --workspace       ä»£ç†ç¨‹åºè¿è¡Œè·¯å¾„ï¼Œé»˜è®¤ä¸ºå½“å‰è„šæœ¬æ‰€åœ¨ç›®å½•
@REM @echo. if this line contains Chinese output, it will be garbled. Why? ? ? >_<
@echo -y, --yacd            ä½¿ç”¨ %ESC%[!warncolor!myacd%ESC%[0m æ§åˆ¶é¢æ¿ï¼Œæ­é… %ESC%[!warncolor!m-i%ESC%[0m æˆ– %ESC%[!warncolor!m-u%ESC%[0m ä½¿ç”¨
@echo.

set "shouldexit=1"
goto :eof


@REM draw heart
:printheart
set "wthitespace="  

@echo.
@echo !wthitespace!        *********           *********
@echo !wthitespace!    *****************   *****************
@echo !wthitespace!  *****************************************
@echo !wthitespace! *******************************************
@echo !wthitespace!*********************************************
@echo !wthitespace!**********************************************
@echo !wthitespace!**********************************************
@echo !wthitespace!**********************************************
if "!msterminal!" == "1" (
    @echo !wthitespace!***********  %ESC%[91mæˆ‘çš„å®ï¼Œæˆ‘çˆ±ä½  â™¥â™¥â™¥%ESC%[0m  *************
) else (
    @echo !wthitespace!*********** %ESC%[91mæˆ‘çš„å®ï¼Œæˆ‘çˆ±ä½  â™¥â™¥â™¥%ESC%[0m ***************
)

@echo !wthitespace!**********                        ***********
@echo !wthitespace! ******** %ESC%[91må› ä¸ºæœ‰ä½ ï¼Œç”Ÿæ´»å¯çˆ±äº†è®¸å¤š%ESC%[0m *********
@echo !wthitespace!  *****************************************
@echo !wthitespace!   ***************************************
@echo !wthitespace!    *************************************
@echo !wthitespace!     ***********************************
@echo !wthitespace!      *********************************
@echo !wthitespace!        *****************************
@echo !wthitespace!          *************************
@echo !wthitespace!            *********************
@echo !wthitespace!               ***************
@echo !wthitespace!                  *********
@echo !wthitespace!                     ***
@echo !wthitespace!                      *
@echo.
exit /b
goto :eof


@REM confirm download url and filename according parameters
:versioned <geosite> <subfiles>
set "%~1=0"
set "content="
set "needgeosite=0"

@REM yacd dashboard
if "!yacd!" == "0" if "!dashboard!" NEQ "" if exist "!dashboard!\registerSW.js" (set "yacd=1")

@REM force use clash.premium
if "!clashpremium!" == "1" (
    set "clashmeta=0"
    goto :eof
)

for /f "tokens=*" %%i in ('findstr /i /r "GEOSITE,.*" "!configfile!"') do set "content=!content!;%%i"
call :searchrules notfound "!content!"

if "!notfound!" == "1" (
    for /f "tokens=*" %%i in ('findstr /i /r "SUB-RULE,.* AND,.* OR,.* NOT,.* IN-TYPE,.*" "!configfile!"') do set "content=!content!;%%i"
    call :searchrules notfound "!content!"
) else (
    set "needgeosite=1"
)

@REM rulesets include GEOSITE, must be clash.meta
if "!notfound!" == "0" (set "clashmeta=1")
if "!clashmeta!" == "1" (
    set "clashmeta=1"
    set "%~1=!needgeosite!"
    goto :eof
)

@REM clash.meta not support SCRIPT rule
set "content="
for /f "tokens=*" %%i in ('findstr /i /r "SCRIPT,.*" "!configfile!"') do set "content=!content!;%%i"
call :searchrules notfound "!content!"

@REM rulesets include SCRIPT, must be clash.premium
if "!notfound!" == "0" (
    set "clashmeta=0"
    goto :eof
)

@REM include sniffer, must be clash.meta
for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"sniffer:[ ]*" "!configfile!"') do (
    call :trim sniffer %%a
    if "!sniffer!" == "sniffer" (
        set "clashmeta=1"
        goto :eof
    )
)

@REM proxy-groups include exclude-filter, must be clash.meta
for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"^[ ][ ]*exclude-filter:[ ][ ]*.*" "!configfile!"') do (
    call :trim excludekey %%a

    if /i "!excludekey:~0,1!" NEQ "#" (
        set "clashmeta=1"
        goto :eof
    )
)

@REM include vless or hysteria, must be clash.meta
call :trim subfiles "%~2"

set "subfiles=!configfile!,!subfiles!"
set "tempfile=!temp!\clashproxies.txt"
set "regex=^\s+(type:\s+(vless|hysteria)|client-fingerprint:\s+|flow:\s+xtls-).*"

del /f /q "!tempfile!" >nul 2>nul
for %%f in (!subfiles!) do (
    if "%%f" NEQ "" if exist %%f (
        call :findby "%%f" "!regex!" "!tempfile!" 1
        if exist "!tempfile!" (
            set "clashmeta=1"
            del /f /q "!tempfile!" >nul 2>nul
            goto :eof
        )   
    )
)

@REM proxy-groups include filter, must be clash.meta
@REM set "tempfile=!temp!\clashproxygroups.txt"
@REM set "regex=^\s+type:\s+(select|url-test|fallback|load-balance|relay).*"

@REM del /f /q "!tempfile!" >nul 2>nul
@REM call :findby "!configfile!" "!regex!" "!tempfile!" 10
@REM if exist "!tempfile!" (
@REM     for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"^[ ][ ]*filter:[ ][ ]*.*" "!tempfile!"') do (
@REM         call :trim includekey %%a
@REM         if /i "!includekey:~0,1!" NEQ "#" (
@REM             set "clashmeta=1"
@REM             del /f /q "!tempfile!" >nul 2>nul
@REM             goto :eof
@REM         )
@REM     )

@REM     del /f /q "!tempfile!" >nul 2>nul
@REM )

@REM old edittion
if exist "!dest!\clash.exe" ("!dest!\clash.exe" -v | findstr /i "Meta" >nul 2>nul && (set "clashmeta=1"))
goto :eof


@REM quickly update subscriptions and rulesets
:quickupdate <edition>
set "%~1=0"

@REM configration
call :updateconfig 1

@REM subscriptions
if "!exclude!" == "0" call :updatesubs subfiles 1

@REM rulesets
call :updaterules 1

@REM detect new edition
set "clashedition=0"
if exist "!dest!\clash.exe" ("!dest!\clash.exe" -v | findstr /i "Meta" >nul 2>nul && (set "clashedition=1"))
call :versioned geositeneed !subfiles!

if "!clashedition!" NEQ "!clashmeta!" (
    set "%~1=1"
    if "!clashmeta!" == "1" (
        set "oldedition=clash.premium"
        set "newedition=clash.meta"
    ) else (
        set "oldedition=clash.meta"
        set "newedition=clash.premium"
    )
    @echo [%ESC%[!warncolor!mæç¤º%ESC%[0m] é…ç½®%ESC%[91mä¸å…¼å®¹%ESC%[0mï¼Œä»£ç†ç¨‹åºéœ€ä» %ESC%[!warncolor!m!oldedition!%ESC%[0m åˆ‡æ¢è‡³ %ESC%[!warncolor!m!newedition!%ESC%[0m
    goto :eof
)

@REM reload
if "!changed!" == "1" (goto :reload) else (goto :eof)


@REM check if special rules are included
:searchrules <notfound> <text>
set "%~1=1"
set "rulesets=%~2"

for /F "tokens=1* delims=;" %%f in ("!rulesets!") do (
    :: set "rule=%%f"
    call :trim rule "%%f"
    if /i "!rule:~0,1!"=="-" (
        set "%~1=0"
        goto :eof
    )

    if "%%g" NEQ "" call :searchrules %~1 "%%g"
)
goto :eof


@REM update subscriptions
:updatesubs <subfiles> <force>
call :trim force "%~2"
if "!force!" == "" set "force=1"

if "!force!" == "1" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] æ£€æŸ¥å¹¶æ›´æ–°è®¢é˜…ï¼Œä»…åˆ·æ–° %ESC%[!warncolor!mHTTP%ESC%[0m ç±»å‹çš„è®¢é˜…
)

call :filerefresh changed "^\s+health-check:(\s+)?$" "www.gstatic.com cp.cloudflare.com" "!force!" subfiles "proxies"
set "%~1=!subfiles!"
goto :eof


:splitpath <directory> <filename> <filepath>
set "%~1=%~dp3"
set "%~2=%~nx3"

if "!%~1:~-1!" == "\" set "%~1=!%~1:~0,-1!"
goto :eof


@REM to absolute path
:pathconvert <result> <filename>
call :trim filepath %~2
set "%~1="

if "!filepath!" == "" goto :eof

@echo "!filepath!" | findstr ":" >nul 2>nul && (
    set "%~1=!filepath!"
    goto :eof
) || (
    if "!dest!" NEQ "" (set "basedir=!dest!") else (set "basedir=%~dp0")
    if "!basedir:~-1!" == "\" set "basedir=!basedir:~0,-1!"

    set "filepath=!filepath:/=\!"
    if "!filepath:~0,3!" == ".\\" (
        set "%~1=!basedir!\!filepath:~3!"
    ) else if "!filepath:~0,2!" == ".\" (
        set "%~1=!basedir!\!filepath:~2!"
    ) else (
        set "%~1=!basedir!\!filepath!"
    )
)
goto :eof


@REM connectivity
:checkconnect <result> <allowed>
@REM running status
set "%~1=0"
call :trim output "%~2"
if "!output!" == "" set "output=1"

call :isrunning status
if "!status!" == "0" (
    if "!output!" == "1" (
        @echo [%ESC%[!warncolor!mæç¤º%ESC%[0m] ç½‘ç»œ%ESC%[91mä¸å¯ç”¨%ESC%[0mï¼Œä»£ç†ç¨‹åº%ESC%[91må·²é€€å‡º%ESC%[0m
    )

    goto :eof
)

@REM call :systemproxy server
call :generateproxy server

@REM detect network is available
call :isavailable status "!output!" "https://www.google.com" "!server!"
set "%~1=!status!"
goto :eof


@REM check network
:isavailable <result> <allowed> <url> <proxyserver>
set "%~1=0"
call :trim output "%~2"
call :trim url "%~3"
call :trim proxyserver "%~4"

if "!output!" == "" set "output=1"
if "!url!" == "" set "url=https://www.google.com"

@REM check
set "statuscode=000"
if "!proxyserver!" == "" (
    for /f %%a in ('curl --retry 3 --retry-max-time 10 -m 5 --connect-timeout 5 -L -s -o nul -w "%%{http_code}" "!url!"') do set "statuscode=%%a"
) else (
    for /f %%a in ('curl -x !proxyserver! --retry 3 --retry-max-time 10 -m 5 --connect-timeout 5 -L -s -o nul -w "%%{http_code}" "!url!"') do set "statuscode=%%a"
)

if "!statuscode!" == "200" (
    set "%~1=1"
    if "!output!" == "1" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] ä»£ç†ç½‘ç»œä¸å­˜åœ¨é—®é¢˜ï¼Œèƒ½å¤Ÿ%ESC%[!infocolor!mæ­£å¸¸%ESC%[0mä½¿ç”¨
    )
) else (
    set "%~1=0"
    if "!output!" == "1" (
        call :postprocess

        @echo [%ESC%[!warncolor!mæç¤º%ESC%[0m] ä»£ç†ç½‘ç»œ%ESC%[91mä¸å¯ç”¨%ESC%[0mï¼Œå¯%ESC%[!warncolor!må†æ¬¡æµ‹è¯•%ESC%[0mæˆ–ä½¿ç”¨å‘½ä»¤ "%ESC%[!warncolor!m!batname! -o%ESC%[0m" %ESC%[!warncolor!mé‡è½½%ESC%[0m æˆ–è€… "%ESC%[!warncolor!m!batname! -r%ESC%[0m" %ESC%[!warncolor!mé‡å¯%ESC%[0m æˆ–è€… "%ESC%[!warncolor!m!batname! -f%ESC%[0m" %ESC%[!warncolor!mä¿®å¤%ESC%[0m
    )
)
goto :eof


@REM query proxy address
:generateproxy <result>
set "%~1="

call :systemproxy server
if "!server!" NEQ "" (
    set "%~1=!server!"
    goto :eof
)

@REM extract from config file
if exist "!configfile!" (
    call :istunenabled enabled
    if "!enabled!" == "1" goto :eof
    call :extractport port
    if "!port!" == "" goto :eof

    set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] ç³»ç»Ÿä»£ç†%ESC%[91mæœªé…ç½®%ESC%[0mï¼Œæ˜¯å¦è®¾ç½®ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
    if "!msterminal!" == "1" (
        choice /t 5 /d y /n /m "!tips!"
    ) else (
        set /p "=!tips!" <nul
        choice /t 5 /d y /n
    )
    if !errorlevel! == 2 goto :eof

    call :enableproxy "127.0.0.1:!port!"
    set "%~1=127.0.0.1:!port!"
    goto :eof
)
goto :eof


@REM create if directory not exists
:makedirs <result> <directory>
set "%~1=0"
call :trim directory "%~2"
if "!directory!" == "" (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] è·¯å¾„ä¸ºç©ºï¼Œåˆ›å»ºç›®å½•å¤±è´¥
    goto :eof
)

if not exist "!directory!" (
    mkdir "!directory!"
    if "!errorlevel!" == "0" set "%~1=1"
) else (set "%~1=1")
goto :eof


@REM tun enabled
:istunenabled <enabled>
set "%~1=0"
set "text="

@REM not work in batch but works fine in cmd, why?
@REM for /f "tokens=*" %%a in ('findstr /i /r /c:"^tun:[ ]*" "!configfile!"') do set "text=%%a"

for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"tun:[ ]*" "!configfile!"') do set "text=%%a"

@REM not required
call :trim text "!text!"
if "!text!" == "tun" set "%~1=1"
goto :eof


@REM wintun
:downloadwintun <changed> <force>
set "%~1=0"

call :trim force "%~2"
if "!force!" == "" set "force=0"

@REM has been integrated in clash.meta
if "!clashmeta!" == "1" exit /b

@REM check if required
call :istunenabled enabled
if "!enabled!" == "0" exit /b

if "!force!" == "0" set "checkwintun=0"

@REM exists
if exist "!dest!\wintun.dll" if "!checkwintun!" == "0" goto :eof

set "content="
set "wintunurl=https://www.wintun.net"

for /f delims^=^"^ tokens^=2 %%a in ('curl --retry 5 --retry-max-time 60 --connect-timeout 15 -s -L "!wintunurl!" ^| findstr /i /r "builds/wintun-.*.zip"') do set "content=%%a"
call :trim content !content!

if "!content!" == "" (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] æ— æ³•è·å– wintun ä¸‹è½½é“¾æ¥
    goto :eof
)

set "wintunurl=!wintunurl!/!content!"
@echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹ä¸‹è½½ wintunï¼Œä¸‹è½½é“¾æ¥ï¼š"!wintunurl!"
curl.exe --retry 5 --retry-max-time 60 --connect-timeout 15 -s -L -C - -o "!temp!\wintun.zip" "!wintunurl!"
if exist "!temp!\wintun.zip" (
    @REM unzip
    tar -xzf "!temp!\wintun.zip" -C !temp! >nul 2>nul

    @REM clean workspace
    del /f /q "!temp!\wintun.zip" >nul 2>nul

    set "wintunfile=!temp!\wintun\bin\amd64\wintun.dll"
    if exist "!wintunfile!" (
        @REM compare and update
        call :md5compare diff "!wintunfile!" "!dest!\wintun.dll"
        if "!diff!" == "1" (
            set "%~1=1"

            @REM delete if exist
            del /f /q "!dest!\wintun.dll" >nul 2>nul
            move "!wintunfile!" "!dest!" >nul 2>nul
        )
    ) else (
        @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] ä¸‹è½½ wintun æˆåŠŸï¼Œä½†æœªæ‰¾åˆ° wintun.dll
    )
) else (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] wintun ä¸‹è½½å¤±è´¥ï¼Œè¯·ç¡®è®¤ä¸‹è½½é“¾æ¥æ˜¯å¦æ­£ç¡®
)
goto :eof


@REM download binary file and data
:donwloadfiles <filenames> <outenable>
set "%~1="
call :trim outenable "%~2"
if "!outenable!" == "" set "outenable=1"

@REM deprecated and no longer needed, so set it to 0
set "outenable=0"

if "!outenable!" == "1" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹ä¸‹è½½ clash.exeã€åŸŸååŠ IP åœ°å€ç­‰æ•°æ®
)

set "dfiles="

@REM download clash
if "!clashurl!" NEQ "" (
    if /i "!clashurl:~0,8!" NEQ "https://" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] clash.exe ä¸‹è½½åœ°å€è§£æå¤±è´¥ï¼š"!clashurl!"
    ) else (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹ä¸‹è½½ %ESC%[!warncolor!mclash.exe%ESC%[0m è‡³ %ESC%[!warncolor!m!dest!%ESC%[0m

        curl.exe --retry 5 --retry-max-time 120 --connect-timeout 20 -s -L -C - -o "!temp!\clash.zip" "!clashurl!"

        if exist "!temp!\clash.zip" (
            @REM unzip
            tar -xzf "!temp!\clash.zip" -C !temp! >nul 2>nul

            @REM clean workspace
            del /f /q "!temp!\clash.zip"
        ) else (
            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] clash.exe ä¸‹è½½å¤±è´¥ï¼Œä¸‹è½½é“¾æ¥ï¼š"!clashurl!"
        )

        if exist "!temp!\!clashexe!" (
            @REM rename file
            ren "!temp!\!clashexe!" clash.exe

            set "dfiles=clash.exe"
        ) else (
            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] "!temp!\!clashexe!" ä¸å­˜åœ¨ï¼Œä¸‹è½½é“¾æ¥ï¼š"!clashurl!"
        )
    )
)

@REM download Country.mmdb
if "!countryurl!" NEQ "" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹ä¸‹è½½ %ESC%[!warncolor!m!countryfile!%ESC%[0m è‡³ %ESC%[!warncolor!m!dest!%ESC%[0m

    curl.exe --retry 5 --retry-max-time 120 --connect-timeout 20 -s -L -C - -o "!temp!\!countryfile!" "!countryurl!"
    if exist "!temp!\!countryfile!" (
        if "!dfiles!" == "" (
            set "dfiles=!countryfile!"
        ) else (
            set "dfiles=!dfiles!;!countryfile!"
        )
    ) else (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] "!temp!\!countryfile!" ä¸å­˜åœ¨ï¼Œä¸‹è½½é“¾æ¥ï¼š"!countryurl!"
    )
)

@REM download GeoSite.dat
if "!geositeurl!" NEQ "" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹ä¸‹è½½ %ESC%[!warncolor!m!geositefile!%ESC%[0m è‡³ %ESC%[!warncolor!m!dest!%ESC%[0m

    curl.exe --retry 5 --retry-max-time 120 --connect-timeout 20 -s -L -C - -o "!temp!\!geositefile!" "!geositeurl!"

    if exist "!temp!\!geositefile!" (
        if "!dfiles!" == "" (
            set "dfiles=!geositefile!"
        ) else (
            set "dfiles=!dfiles!;!geositefile!"
        )
    ) else (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] "!temp!\!geositefile!" ä¸å­˜åœ¨ï¼Œä¸‹è½½é“¾æ¥ï¼š"!geositeurl!"
    )
)

@REM download GeoIP.dat
if "!geoipurl!" NEQ "" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹ä¸‹è½½ %ESC%[!warncolor!m!geoipfile!%ESC%[0m è‡³ %ESC%[!warncolor!m!dest!%ESC%[0m

    curl.exe --retry 5 --retry-max-time 120 --connect-timeout 20 -s -L -C - -o "!temp!\!geoipfile!" "!geoipurl!"

    if exist "!temp!\!geoipfile!" (
        if "!dfiles!" == "" (
            set "dfiles=!geoipfile!"
        ) else (
            set "dfiles=!dfiles!;!geoipfile!"
        )
    ) else (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] "!temp!\!geoipfile!" ä¸å­˜åœ¨ï¼Œä¸‹è½½é“¾æ¥ï¼š"!geoipurl!"
    )
)

set "%~1=!dfiles!"
goto :eof


@REM compare
:detect <result> <filenames>
set "%~1=0"
set "filenames=%~2"

for %%a in (!filenames!) do (
    set "fname=%%a"

    if not exist "!temp!\!fname!" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] %ESC%[!warncolor!m!fname!%ESC%[0m ä¸‹è½½æˆåŠŸï¼Œä½†åœ¨ "!temp!" æ–‡ä»¶å¤¹ä¸‹æœªæ‰¾åˆ°ï¼Œè¯·ç¡®è®¤æ˜¯å¦å·²è¢«åˆ é™¤
        goto :eof
    )

    if "!repair!" == "1" (
        @REM delete for triggering upgrade
        del /f /q "!dest!\!fname!" >nul 2>nul
    )

    @REM found new file
    if not exist "!dest!\!fname!" (
        set "%~1=1"
        call :upgrade "!filenames!"
        exit /b
    )

    @REM compare and update
    call :md5compare diff "!temp!\!fname!" "!dest!\!fname!"
    if "!diff!" == "1" (
        set "%~1=1"
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å‘ç°æ–°ç‰ˆæœ¬ï¼Œæ–‡ä»¶åï¼š%ESC%[!warncolor!m!fname!%ESC%[0m
        call :upgrade "!filenames!"
        exit /b
    )
)
goto :eof


@REM compare file with md5
:md5compare <changed> <source> <target>
set "%~1=0"

call :trim source "%~2"
call :trim target "%~3"

if not exist "!source!" if not exist "!target!" goto :eof
if not exist "!source!" goto :eof
if not exist "!target!" (
    set "%~1=1"
    goto :eof
)

@REM source md5
set "original=" & for /F "skip=1 delims=" %%h in ('2^> nul CertUtil -hashfile "!source!" MD5') do if not defined original set "original=%%h"
@REM target md5
set "received=" & for /F "skip=1 delims=" %%h in ('2^> nul CertUtil -hashfile "!target!" MD5') do if not defined received set "received=%%h"

if "!original!" NEQ "!received!" (set "%~1=1")
goto :eof


@REM update clash.exe and data
:upgrade <filenames>
call :trim filenames "%~1"
if "!filenames!" == "" goto :eof

@REM make sure the file exists
set "existfiles="
for %%a in (!filenames!) do (
    if exist "!temp!\%%a" (
        if "!existfiles!" == "" (
            set "existfiles=%%a"
        ) else (
            set "existfiles=!existfiles!;%%a"
        )
    )
)

@REM file missing
if "!existfiles!" == "" goto :terminate

@REM stop clash
call :killprocesswrapper

@REM copy file
for %%a in (!filenames!) do (
    set "fname=%%a"

    @REM delete if old file exists
    if exist "!dest!\!fname!" (
        del /f /q "!dest!\!fname!" >nul 2>nul
    )
    
    @REM move new file to dest
    move "!temp!\!fname!" "!dest!" >nul 2>nul
)
goto :eof


@REM start
:startclash
call :isrunning status

if "!status!" == "0" (
    @REM startup clash
    call :executewrapper 0
) else (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] è®¢é˜…å’Œä»£ç†è§„åˆ™æ›´æ–°å®Œæ¯•ï¼Œå³å°†é‡æ–°åŠ è½½
    goto :reload
)
goto :eof


@REM privilege escalation
:privilege <args> <show>
set "hidewindow=0"
set "operation=%~1"
if "!operation!" == "" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] éæ³•æ“ä½œï¼Œå¿…é¡»æŒ‡å®šå‡½æ•°å
    exit /b 1
)

@REM parse window parameter
call :trim param "%~2"
set "display=" & for /f "delims=0123456789" %%i in ("!param!") do set "display=%%i"
if defined display (set "hidewindow=0") else (set "hidewindow=!param!")
if "!hidewindow!" NEQ "0" set "hidewindow=1"

cacls "%SystemDrive%\System Volume Information" >nul 2>&1 && (
    if "!hidewindow!" == "1" (
        !operation!
        exit /b
    ) else (
        start "" mshta vbscript:CreateObject^("Shell.Application"^).ShellExecute^("%~snx0","%~1","","runas",0^)^(window.close^)&exit /b
    )
) || (start "" mshta vbscript:CreateObject^("Shell.Application"^).ShellExecute^("%~snx0","%~1","","runas",!hidewindow!^)^(window.close^)&exit /b)
goto :eof


@REM execute
:execute <config>
call :trim cfile "%~1"
if "!cfile:~0,13!" == "goto :execute" (
    for /f "tokens=1-4 delims= " %%a in ("!cfile!") do set "cfile=%%c"
)

if "!cfile!" == "" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] é…ä»¶æ–‡ä»¶è·¯å¾„æ— æ•ˆï¼Œæ— æ³•å¯åŠ¨ä»£ç†ç¨‹åº
    goto :eof
)

@REM privilege escalation
call :nopromptrunas success

call :splitpath filepath filename "!cfile!" 
"!filepath!\clash.exe" -d "!filepath!" -f "!cfile!"
goto :eof


@REM ensure all plugins exist
:prepare <changed> <force> <downloaded>
set "%~1=0"

call :trim downforce "%~2"
if "!downforce!" == "" set "downforce=0"

call :trim downloaded "%~3"
if "!downloaded!" == "" set "downloaded=0"

@REM check and update configration
if "!downloaded!" == "0" call :updateconfig "!downforce!"

@REM dashboard directory name
call :extractpath dashboard

@REM update subscriptions
if "!downloaded!" == "0" if "!exclude!" == "0" call :updatesubs subfiles "!downforce!"

@REM confirm download url and filename
call :versioned geositeneed !subfiles!

@REM confirm donwload url
call :confirmurl "!downforce!" "!geositeneed!"

@REM precleann workspace
call :cleanworkspace "!temp!"

@REM update dashboard
if "!downloaded!" == "0" call :dashboardupdate "!downforce!"

@REM update rulefiles
if "!downloaded!" == "0" call :updaterules "!downforce!"

@REM wintun.dll
call :downloadwintun newwintun "!downforce!"
set "%~1=!newwintun!"

@REM download clah.exe and geoip.data and so on
call :donwloadfiles filenames "!downforce!"

@REM judge file changed with md5
call :detect changed "!filenames!"
if "!changed!" == "1" set "%~1=!changed!"

goto :eof


@REM config autostart and auto update
:postprocess
call :privilege "goto :nopromptrunas" 0

@REM tips
call :outputhint

@REM allow auto start when user login
call :autostart

@REM allow auto check update
call :autoupdate

@REM create shortcut on desktop
call :adddesktop
goto :eof


@REM privilege escalation
:executewrapper <shouldcheck>
call :trim shouldcheck "%~1"
if "!shouldcheck!" == "" set "shouldcheck=0"
if "!shouldcheck!" == "1" (call :prepare changed 0 0)

@REM verify config
if not exist "!dest!\clash.exe" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œä»£ç†å¯åŠ¨%ESC%[91må¤±è´¥%ESC%[0mï¼Œ"%ESC%[!warncolor!m!dest!\clash.exe%ESC%[0m" ç¼ºå¤±
    goto :eof
)

if not exist "!configfile!" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œä»£ç†å¯åŠ¨%ESC%[91må¤±è´¥%ESC%[0mï¼Œé…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!configfile!%ESC%[0m" ä¸å­˜åœ¨
    goto :eof
)

if "!verifyconf!" == "1" (
    set "testoutput=!temp!\clashtestout.txt"
    del /f /q "!testoutput!" >nul 2>nul

    @REM test config file
    "!dest!\clash.exe" -d "!dest!" -t "!configfile!" > "!testoutput!"

    @REM failed
    if !errorlevel! NEQ 0 (
        set "messages="
        if exist "!testoutput!" (
            for /f "tokens=1* delims==" %%a in ('findstr /i /r /c:"[ ]ERR[ ]\[config\][ ].*" "!testoutput!"') do set "messages=%%b"
            del /f /q "!testoutput!" >nul 2>nul
        )

        if "!messages!" == "" set "messages=æ–‡ä»¶æ ¡éªŒå¤±è´¥ï¼Œ%ESC%[!warncolor!mclash.exe%ESC%[0m æˆ–é…ç½®æ–‡ä»¶ %ESC%[!warncolor!m!configfile!%ESC%[0m å­˜åœ¨é—®é¢˜"
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œä»£ç†å¯åŠ¨%ESC%[91må¤±è´¥%ESC%[0mï¼Œé…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!configfile!%ESC%[0m" å­˜åœ¨é”™è¯¯
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] é”™è¯¯ä¿¡æ¯ï¼š"!messages!"
        exit /b 1
    )

    @REM delete test output
    del /f /q "!testoutput!" >nul 2>nul
)

@REM run clash.exe with config
call :privilege "goto :execute !configfile!" !show!

for /l %%i in (1,1,6) do (
    @REM check running status
    call :isrunning status
    if "!status!" == "1" (
        @REM abnormal detect
        call :abnormal state

        if "!state!" == "1" (
            set "tips=[%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] ä»£ç†è¿›ç¨‹%ESC%[91må¼‚å¸¸%ESC%[0mï¼Œéœ€%ESC%[91måˆ é™¤å¹¶é‡æ–°ä¸‹è½½%ESC%[0m %ESC%[!warncolor!m!dest!\clash.exe%ESC%[0mï¼Œæ˜¯å¦ç»§ç»­ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
            if "!msterminal!" == "1" (
                choice /t 5 /d y /n /m "!tips!"
            ) else (
                set /p "=!tips!" <nul
                choice /t 5 /d y /n
            )
            if !errorlevel! == 1 (
                @REM delete exist clash.exe
                del /f /q "!dest!\clash.exe" >nul 2>nul

                @REM download and restart
                goto :restartprogram
            ) else (
                @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ä»£ç†ç¨‹åºå¯åŠ¨%ESC%[91må¤±è´¥%ESC%[0mï¼Œè¯·æ£€æŸ¥ä»£ç†ç¨‹åº %ESC%[!warncolor!m!dest!\clash.exe%ESC%[0m æ˜¯å¦å®Œå¥½
                goto :eof
            )
        ) else (
            @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] ä»£ç†ç¨‹åºå¯åŠ¨%ESC%[!infocolor!mæˆåŠŸ%ESC%[0m
            call :postprocess
            exit /b
        )
    ) else (
        @REM waiting
        timeout /t 1 /nobreak >nul 2>nul
    )
)

@echo [%ESC%[91mé”™è¯¯%ESC%[0m] ä»£ç†ç¨‹åºå¯åŠ¨%ESC%[91må¤±è´¥%ESC%[0mï¼Œè¯·æ£€æŸ¥é…ç½® %ESC%[91mconfiguration%ESC%[0m æ˜¯å¦æ­£ç¡®
goto :eof


@REM search port on config file with keyword
:searchport <result> <key>
set "%~1="
set "content="
call :trim key "%~2"
if "!key!" == "" goto :eof

@REM search
for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"^^!key!:[ ][ ]*[0-9][0-9]*" "!configfile!"') do set "content=%%b"
if "!content!" == "" goto :eof

call :trim port "!content!"
if "!port!" NEQ "" set "%~1=!port!"
goto :eof


@REM extract proxy port
:extractport <result>
set "%~1=7890"
set "keys=mixed-port;port;socks-port"
for %%a in (!keys!) do (
    call :searchport port "%%a"
    if "!port!" NEQ "" (
        set "%~1=!port!"
        exit /b
    )
)
goto :eof


@REM print warning if tun is disabled
:outputhint
call :istunenabled enabled
call :systemproxy server
if "!enabled!" == "1" (
    if "!server!" NEQ "" (
        @echo [%ESC%[!warncolor!mæç¤º%ESC%[0m] ç¨‹åºæ­£ä»¥ %ESC%[!warncolor!mtun%ESC%[0m æ¨¡å¼è¿è¡Œï¼Œç³»ç»Ÿä»£ç†è®¾ç½®å·²è¢«ç¦ç”¨
        call :disableproxy
    )
    goto :eof
)

call :extractport proxyport
if "!proxyport!" == "" set "proxyport=7890"

@REM set proxy
set "proxyserver=127.0.0.1:!proxyport!"
if "!proxyserver!" NEQ "!server!" (
    set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] ç³»ç»Ÿä»£ç†%ESC%[91mæœªé…ç½®%ESC%[0mï¼Œæ˜¯å¦è®¾ç½®ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
    if "!msterminal!" == "1" (
        choice /t 5 /d y /n /m "!tips!"
    ) else (
        set /p "=!tips!" <nul
        choice /t 5 /d y /n
    )
    if !errorlevel! == 1 call :enableproxy "!proxyserver!"
)

@REM hint
@echo [%ESC%[!warncolor!mæç¤º%ESC%[0m] å¦‚æœæ— æ³•æ­£å¸¸ä½¿ç”¨ç½‘ç»œä»£ç†ï¼Œè¯·åˆ° "%ESC%[!warncolor!mè®¾ç½® -^> ç½‘ç»œå’Œ Internet -^> ä»£ç†%ESC%[0m" ç¡®è®¤æ˜¯å¦å·²è®¾ç½®ä¸º "%ESC%[!warncolor!m!proxyserver!%ESC%[0m"
goto :eof


@REM restart program
:restartprogram
@REM check running status
call :isrunning status
if "!status!" == "1" (
    @REM kill process
    call :killprocesswrapper

    @REM check running status
    call :isrunning status

    if "!status!" == "1" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ³•å…³é—­è¿›ç¨‹ï¼Œä»£ç†ç¨‹åºé‡å¯%ESC%[91må¤±è´¥%ESC%[0mï¼Œè¯·åˆ°%ESC%[91mä»»åŠ¡ç®¡ç†ä¸­å¿ƒ%ESC%[0mæ‰‹åŠ¨é€€å‡º %ESC%[!warncolor!mclash.exe%ESC%[0m
        goto :eof
    )
)

@REM if alpha=1 may cause clash download failure
set "alpha=0"

@REM startup
call :executewrapper 1
exit /b


@REM run as admin
:killprocesswrapper
call :isrunning status
if "!status!" == "0" goto :eof

call :privilege "goto :killprocess" 0

@REM detect
for /l %%i in (1,1,6) do (
    call :isrunning status
    if "!status!" == "0" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] ä»£ç†ç¨‹åºå…³é—­%ESC%[!infocolor!mæˆåŠŸ%ESC%[0mï¼Œå¯ä½¿ç”¨ "%ESC%[!warncolor!m!batname! -r%ESC%[0m" å‘½ä»¤é‡å¯

        @REM disable proxy
        @REM call :istunenabled enabled
        @REM if "!enabled!" == "0" call :disableproxy

        call :disableproxy
        exit /b
    ) else (
        @REM wait a moment
        timeout /t 1 /nobreak >nul 2>nul
    )
)

@echo [%ESC%[91mé”™è¯¯%ESC%[0m] ä»£ç†ç¨‹åºå…³é—­%ESC%[91må¤±è´¥%ESC%[0mï¼Œè¯·åˆ°%ESC%[91mä»»åŠ¡ç®¡ç†ä¸­å¿ƒ%ESC%[0mæ‰‹åŠ¨é€€å‡º %ESC%[!warncolor!mclash.exe%ESC%[0m
goto :eof


@REM stop
:killprocess
tasklist | findstr /i "clash.exe" >nul 2>nul && taskkill /im "clash.exe" /f >nul 2>nul
set "exitcode=!errorlevel!"

@REM no prompt
call :nopromptrunas success

@REM detect
for /l %%i in (1,1,6) do (
    @REM detect running status
    call :isrunning status
    if "!status!" == "0" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] ç½‘ç»œä»£ç†å·²å…³é—­
        goto :eof
    ) else (
        @REM waiting for release
        timeout /t 1 /nobreak >nul 2>nul
    )
)

@echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œä»£ç†å…³é—­å¤±è´¥ï¼Œè¯·åˆ°%ESC%[91mä»»åŠ¡ç®¡ç†ä¸­å¿ƒ%ESC%[0mæ‰‹åŠ¨ç»“æŸ %ESC%[!warncolor!mclash.exe%ESC%[0m è¿›ç¨‹
goto :eof


@REM delect running status
:isrunning <result>
tasklist | findstr /i "clash.exe" >nul 2>nul && set "%~1=1" || set "%~1=0"
goto :eof


@REM check clash.exe process is normal
:abnormal <result>
set "%~1=1"

@REM memory usage
set "usage="

for /f "tokens=5 delims= " %%a in ('tasklist /nh ^|findstr /i clash.exe') do set "usage=%%a"
if "!usage!" NEQ "" (
    @REM remove comma from number
    set "usage=!usage:,=!"

    if !usage! GTR 5120 (set "%~1=0")
)

goto :eof


@REM get donwload url
:confirmurl <force> <enabled>
@REM country data
call :trim force "%~1"
if "!force!" == "" set "force=0"

call :trim geositeflag "%~2"
if "!geositeflag!" == "" set "geositeflag=0"

set "needdownload=0"
set "countryurl=https://raw.githubusercontent.com/Hackl0us/GeoIP2-CN/release/Country.mmdb"

@REM geosite/geoip filename
set "countryfile=Country.mmdb"
set "geositefile=GeoSite.dat"
set "geoipfile=GeoIP.dat"

@REM dashboard url
set "dashboardurl=https://github.com/wzdnzd/clash-dashboard/archive/refs/heads/gh-pages.zip"
set "dashdirectory=clash-dashboard-gh-pages"

set "clashurl="

@REM determine whether to download clash.exe
if not exist "!dest!\clash.exe" (set "needdownload=1") else (set "needdownload=!force!")

if "!clashmeta!" == "0" (
    set "clashexe=clash-windows-amd64.exe"

    if "!needdownload!" == "1" (
        if "!alpha!" == "0" (
            for /f "tokens=1* delims=:" %%a in ('curl --retry 5 -s -L "https://api.github.com/repos/Dreamacro/clash/releases/tags/premium" ^| findstr /i /r /c:"https://github.com/Dreamacro/clash/releases/download/premium/clash-windows-amd64-[^v][^3].*.zip"') do set "clashurl=%%b"
            
            @REM remove whitespace
            call :trim clashurl "!clashurl!"
            if !clashurl! == "" (
                @echo [%ESC%[91mé”™è¯¯%ESC%[0m] è·å– clash.premium ä¸‹è½½é“¾æ¥å¤±è´¥
                goto :eof
            )
            set "clashurl=!clashurl:~1,-1!"
        ) else (
            set "clashurl=https://release.dreamacro.workers.dev/latest/clash-windows-amd64-latest.zip"
        )
    )

    if "!yacd!" == "1" (
        @REM set "dashboardurl=https://github.com/haishanh/yacd/archive/refs/heads/gh-pages.zip"
        set "dashboardurl=https://github.com/wzdnzd/Yacd/archive/refs/heads/gh-pages.zip"
        set "dashdirectory=yacd-gh-pages"
    )
) else (
    set "clashexe=Clash.Meta-windows-amd64.exe"
    set "geositeurl=https://raw.githubusercontent.com/Loyalsoldier/domain-list-custom/release/geosite.dat"
    set "geoipurl=https://raw.githubusercontent.com/Loyalsoldier/geoip/release/geoip-only-cn-private.dat"

    if "!needdownload!" == "1" (
        if "!alpha!" == "1" (
            for /f "tokens=1* delims=:" %%a in ('curl --retry 5 -s -L "https://api.github.com/repos/MetaCubeX/Clash.Meta/releases?prerelease=true&per_page=10" ^| findstr /i /r "https://github.com/MetaCubeX/Clash.Meta/releases/download/Prerelease-Alpha/Clash.Meta-windows-amd64-alpha-.*.zip"') do set "clashurl=%%b"
        ) else (
            for /f "tokens=1* delims=:" %%a in ('curl --retry 5 -s -L "https://api.github.com/repos/MetaCubeX/Clash.Meta/releases/latest?per_page=1" ^| findstr /i /r "https://github.com/MetaCubeX/Clash.Meta/releases/download/.*/Clash.Meta-windows-amd64-.*.zip"') do set "clashurl=%%b"
        )

        call :trim clashurl "!clashurl!"
        if !clashurl! == "" (
            if "!alpha!" == "1" (set "version=é¢„è§ˆç‰ˆ") else (set "version=ç¨³å®šç‰ˆ")
            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] è·å– clash.meta ä¸‹è½½é“¾æ¥å¤±è´¥ï¼Œç‰ˆæœ¬ï¼š"!version!"
            goto :eof
        )

        set "clashurl=!clashurl:~1,-1!"
    )

    @REM geosite.data download url
    if "!geositeflag!" == "0" (
        set "geositeurl="
    ) else (
        for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"^[ ][ ]*geosite:[ ][ ]*" "!configfile!"') do (
            call :trim geositekey %%a

            @REM commented
            if /i "!geositekey:~0,1!" NEQ "#" call :trim geositeurl %%b
        )
    )

    @REM geodata-mode
    set "geodatamode=false"
    for /f "tokens=1,2 delims=:" %%a in ('findstr /i /r /c:"^geodata-mode:[ ][ ]*" "!configfile!"') do (
        call :trim gmn %%a

        @REM commented
        if /i "!gmn:~0,1!" NEQ "#" call :trim geodatamode %%b
    )

    @REM geoip.data
    if "!geodatamode!" == "false" (
        set "geoipurl="

        for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"^[ ][ ]*mmdb:[ ][ ]*" "!configfile!"') do (
            call :trim mmdbkey %%a

            @REM commented
            if /i "!mmdbkey:~0,1!" NEQ "#" call :trim countryurl %%b
        )
    ) else (
        set "countryurl="

        for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"^[ ][ ]*geoip:[ ][ ]*http.*://" "!configfile!"') do (
            call :trim geoipkey %%a
            
            @REM commented
            if /i "!geoipkey:~0,1!" NEQ "#" call :trim geoipurl %%b
        )
    )

    if "!yacd!" == "1" (
        @REM set "dashboardurl=https://github.com/MetaCubeX/Yacd-meta/archive/refs/heads/gh-pages.zip"
        set "dashboardurl=https://github.com/wzdnzd/Yacd/archive/refs/heads/gh-pages.zip"
        set "dashdirectory=Yacd-meta-gh-pages"
    )
)

@REM clashurl
call :generateurl clashurl "!clashurl!" "clash.exe" "!force!"

@REM dashboardurl
if "!dashboard!" == "" (
    @REM don't need dashboard
    set "dashboardurl="
) else (
    set "needdash=!force!"
    if not exist "!dashboard!\index.html" set "needdash=1"
    if "!needdash!" == "0" (
        set "dashboardurl="
    ) else (
        call :ghproxywrapper dashboardurl !dashboardurl!
    )
)

@REM countryurl
call :generateurl countryurl "!countryurl!" "!countryfile!" "!force!"

@REM geositeurl
call :generateurl geositeurl "!geositeurl!" "!geositefile!" "!force!"

@REM geoipurl
call :generateurl geoipurl "!geoipurl!" "!geoipfile!" "!force!"
goto :eof


@REM generate real download url
:generateurl <result> <url> <filename> <force>
set "%~1="

call :trim url "%~2"
if "!url!" == "" goto :eof

call :trim filename "%~3"
if "!filename!" == "" goto :eof

if not exist "!dest!\!filename!" (set "needdownload=1") else (set "needdownload=!force!")
if "!needdownload!" == "0" goto :eof

call :ghproxywrapper downloadurl !url!

set "%~1=!downloadurl!"
goto :eof


@REM leading and trailing whitespace
:trim <result> <rawtext>
set "rawtext=%~2"
set "%~1="
if "!rawtext!" == "" goto :eof

for /f "tokens=* delims= " %%a in ("!rawtext!") do set "rawtext=%%a"

@REM for /l %%a in (1,1,100) do if "!rawtext:~-1!"==" " set "rawtext=!rawtext:~0,-1!"

@REM for speed, iteration set to 10
for /l %%a in (1,1,10) do if "!rawtext:~-1!"==" " set "rawtext=!rawtext:~0,-1!"

set "%~1=!rawtext!"
goto :eof


@REM wrapper github
:ghproxywrapper <result> <rawurl>
set "%~1="
call :trim rawurl %~2
if "!rawurl!" == "" goto :eof

@REM github proxy list: https://github.com/XIU2/UserScript/blob/master/GithubEnhanced-High-Speed-Download.user.js
set proxy_urls[0]=https://ghproxy.com
set proxy_urls[1]=https://github.moeyy.xyz
set proxy_urls[2]=https://gh.ddlc.top
set proxy_urls[3]=https://ghps.cc
set proxy_urls[4]=https://hub.gitmirror.com

@REM random [0, 4]
set /a num=!random! %% 5
set "ghproxy=!proxy_urls[%num%]!"

@REM github proxy
if "!rawurl:~0,18!" == "https://github.com" set "rawurl=!ghproxy!/!rawurl!"
if "!rawurl:~0,33!" == "https://raw.githubusercontent.com" set "rawurl=!ghproxy!/!rawurl!"
if "!rawurl:~0,34!" == "https://gist.githubusercontent.com" set "rawurl=!ghproxy!/!rawurl!"

set "%~1=!rawurl!"
goto :eof


@REM search keywords with powershell
:findby <filepath> <regex> <resultfile> <lines>
call :trim filepath %~1
if "!filepath!" == "" goto :eof

set "regex=%~2"
if "!regex!" == "" goto :eof

call :trim result %~3
if "!result!" == "" goto :eof

call :trim context %~4
if not defined context (set "context=5")

powershell -command "& {&'Get-Content' '!filepath!' | &'Select-String' -Pattern '!regex!' -Context !context!,!context! | &'Set-Content' -Encoding 'utf8' '!result!'}";
goto :eof


@REM remove leading and trailing quotes
:removequotes <result> <str>
set "%~1="
call :trim str "%~2"
if "!str!" == "" goto :eof

if !str:~0^,1!!str:~-1! equ "" set "str=!str:~1,-1!"
if "!str:~0,1!!str:~0,1!" == "''" set "str=!str:~1!"
if "!str:~-1!!str:~-1!" == "''" set "str=!str:~0,-1!"
set "%~1=!str!"
goto :eof


@REM query value from yaml
:parsevalue <result> <regex>
set "%~1="
set "regex=%~2"
if "!regex!" == "" goto :eof

set "key="
set "text="
for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"!regex!" "!configfile!"') do (
    set "key=%%a"
    set "text=%%b"
)

call :trim key "!key!"
if "!key!" == "" goto :eof
@REM commened 
if "!key:~0,1!" == "#" goto :eof

call :removequotes value "!text!"
set "%~1=!value!"
goto :eof


@REM reload config
:reload
if not exist "!configfile!" goto :eof

@REM clash api address
call :parsevalue clashapi "external-controller:[ ][ ]*"
if "!clashapi!" == "" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] %ESC%[91mä¸æ”¯æŒ%ESC%[0mé‡è½½ï¼Œå¯ä½¿ç”¨ "%ESC%[!warncolor!m!batname! -r%ESC%[0m" é‡å¯æˆ–è€…åœ¨æ–‡ä»¶ "%ESC%[!warncolor!m!configfile!%ESC%[0m" é…ç½® "%ESC%[!warncolor!mexternal-controller%ESC%[0m" å±æ€§ä»¥å¯ç”¨è¯¥åŠŸèƒ½
    goto :eof
)

@REM localhost default
if "!clashapi:~0,1!" == ":" set "clashapi=127.0.0.1!clashapi!"

set "clashapi=http://!clashapi!/configs?force=true"

@REM secret
call :parsevalue secret "secret:[ ][ ]*"

@REM running detect
call :isrunning status

if "!status!" == "1" (
    @REM '\' to '\\'
    set "filepath=!configfile:\=\\!"

    @REM call api for reload
    set "statuscode=000"
    set "output=!temp!\clashout.txt"
    if exist "!output!" del /f /q "!output!" >nul 2>nul

    if "!secret!" NEQ "" (
        for /f %%a in ('curl --retry 3 -L -s -o "!output!" -w "%%{http_code}" -H "Content-Type: application/json" -H "Authorization: Bearer !secret!" -X PUT -d "{""path"":""!filepath!""}" "!clashapi!"') do set "statuscode=%%a"
    ) else (
        for /f %%a in ('curl --retry 3 -L -s -o "!output!" -w "%%{http_code}" -H "Content-Type: application/json" -X PUT -d "{""path"":""!filepath!""}" "!clashapi!"') do set "statuscode=%%a"
    )

    if "!statuscode!" == "204" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] ç½‘ç»œä»£ç†ç¨‹åºé‡è½½%ESC%[!infocolor!mæˆåŠŸ%ESC%[0mï¼Œç¥ä½ ä½¿ç”¨æ„‰å¿«
        call :postprocess
    ) else if "!statuscode!" == "401" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] %ESC%[!warncolor!msecret%ESC%[0m å·²è¢«ä¿®æ”¹ï¼Œè¯·ä½¿ç”¨ "%ESC%[!warncolor!m!batname! -r%ESC%[0m" é‡å¯
    ) else (
        set "content="

        if exist "!output!" (
            @REM read output
            for /f "delims=" %%a in (!output!) do set "content=%%a"
        )

        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œä»£ç†ç¨‹åºé‡è½½%ESC%[91må¤±è´¥%ESC%[0mï¼Œè¯·æ£€æŸ¥é…ç½®æ–‡ä»¶ "%ESC%[!warncolor!m!configfile!%ESC%[0m" æ˜¯å¦æœ‰æ•ˆ
        if "!content!" NEQ "" (
            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] é”™è¯¯ä¿¡æ¯ï¼š"!content!"
        )

        @echo.
    )

    @REM delete
    del /f /q "!output!" >nul 2>nul
) else (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œä»£ç†ç¨‹åº%ESC%[91mæœªå¯åŠ¨%ESC%[0mï¼Œå¯ä½¿ç”¨å‘½ä»¤ "%ESC%[!warncolor!m!batname! -r%ESC%[0m" å¯åŠ¨
)
goto :eof


@REM update config
:updateconfig <force>
call :trim force "%~1"
if "!force!" == "" set "force=1"
if exist "!configfile!" if "!force!" == "0" goto :eof

set "downloadpath=!temp!\clashconf.yaml"
del /f /q "!downloadpath!" >nul 2>nul

@REM extract remote config url
set "subfile=!dest!\subscriptions.txt"
set "subscription="

if exist "!subfile!" (
    for /f "tokens=*" %%a in ('findstr /i /r /c:"^http.*://" "!subfile!"') do set "subscription=%%a"
    if "!subscription!" NEQ "" (
        call :trim subscription "!subscription!"
        if "!subscription:~0,1!" NEQ "#" set "remoteurl=!subscription!"
    )
)

if "!enableremoteconf!" == "1" if "!remoteurl!" NEQ "" (
    curl.exe --retry 5 --retry-max-time 90 -m 120 --connect-timeout 15 -H "User-Agent: Clash" -s -L -C - "!remoteurl!" > "!downloadpath!"
    if not exist "!downloadpath!" (
        @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] é…ç½®æ–‡ä»¶ä¸‹è½½å¤±è´¥ï¼Œå¦‚æœ‰éœ€è¦ï¼Œè¯·é‡è¯•æˆ–ç‚¹å‡» %ESC%[!warncolor!m!remoteurl!%ESC%[0m æ‰‹åŠ¨ä¸‹è½½å¹¶æ›¿æ¢
        goto :eof
    )

    if exist "!dest!\clash.exe" (
        @REM check file
        for %%a in ("!downloadpath!") do set "filesize=%%~za"
        if !filesize! LSS 32 (
            del /f /q "!downloadpath!" >nul 2>nul
            @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] é…ç½®æ–‡ä»¶ä¸‹è½½å¤±è´¥ï¼Œå¦‚æœ‰éœ€è¦ï¼Œè¯·é‡è¯•æˆ–ç‚¹å‡» %ESC%[!warncolor!m!remoteurl!%ESC%[0m æ‰‹åŠ¨ä¸‹è½½å¹¶æ›¿æ¢
            exit /b 1
        )
        
        @REM test config file
        "!dest!\clash.exe" -d "!dest!" -t -f "!downloadpath!" >nul 2>nul

        @REM failed
        if !errorlevel! NEQ 0 (
            @echo [%ESC%[91mé”™è¯¯%ESC%[0m] é…ç½®æ–‡ä»¶ %ESC%[!warncolor!m!remoteurl!%ESC%[0m å­˜åœ¨é”™è¯¯ï¼Œæ— æ³•æ›´æ–°
            del /f /q "!downloadpath!" >nul 2>nul
            exit /b 1
        )
    )

    @REM compare with md5
    call :md5compare diff "!downloadpath!" "!configfile!"
    if "!diff!" == "0" (
        del /f /q "!downloadpath!" >nul 2>nul
        goto :eof
    )

    set "backupfile=config.yaml.bak"
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å‘ç°è¾ƒæ–°é…ç½®ï¼ŒåŸæœ‰æ–‡ä»¶å°†å¤‡ä»½ä¸º %ESC%[!warncolor!m!dest!\!backupfile!%ESC%[0m

    @REM backup
    del /f /q "!dest!\!backupfile!" >nul 2>nul
    ren "!configfile!" !backupfile!

    @REM move new configration file to dest
    move "!downloadpath!" "!configfile!" >nul 2>nul
)
goto :eof


@REM update rules
:updaterules <force>
call :trim force "%~1"
if "!force!" == "" set "force=1"

if "!force!" == "1" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹æ£€æŸ¥å¹¶æ›´æ–°ç±»å‹ä¸º %ESC%[!warncolor!mHTTP%ESC%[0m çš„ä»£ç†è§„åˆ™
)

call :filerefresh changed "^\s+behavior:\s+.*" "www.gstatic.com cp.cloudflare.com" "!force!" rulefiles "payload"
goto :eof


@REM refresh subsribe and rulesets
:filerefresh <result> <regex> <filter> <force> <filepaths> <check>
set "%~1=0"
set "regex=%~2"
set "%~5="

call :trim filter "%~3"
if "!filter!" == "" set "filter=www.gstatic.com cp.cloudflare.com"

call :trim check "%~6"

call :trim force "%~4"
if "!force!" == "" set "force=1"

if "!regex!" == "" (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] æœªæŒ‡å®šå…³é”®ä¿¡æ¯ï¼Œè·³è¿‡æ›´æ–°
    goto :eof
)

set texturls=
set localfiles=

if not exist "!configfile!" goto :eof

@REM temp file
set "tempfile=!temp!\clashupdate.txt"
set "filepaths=" 

call :findby "!configfile!" "!regex!" "!tempfile!" 5
if not exist "!tempfile!" (
    if "!force!" == "0" goto :eof

    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] æœªå‘ç°è®¢é˜…æˆ–ä»£ç†è§„åˆ™ç›¸å…³é…ç½®ï¼Œè·³è¿‡æ›´æ–°ï¼Œæ–‡ä»¶ï¼š"!configfile!"
    goto :eof
)

@REM urls and file path
for /f "tokens=1* delims=:" %%i in ('findstr /i /r /c:"^[ ][ ]*url:[ ][ ]*http.*://.*" !tempfile!') do (
    call :trim propertity %%i
    if "!propertity:~0,1!" NEQ "#" (
        @echo "%%j" | findstr /i "!filter!" >nul 2>nul || set "texturls=!texturls!,%%j"
    )
)

for /f "tokens=1* delims=:" %%i in ('findstr /i /r /c:"^[ ][ ]*path:[ ][ ]*.*" !tempfile!') do (
    call :trim propertity %%i
    if "!propertity:~0,1!" NEQ "#" (
        set "localfiles=!localfiles!,%%j"
    )
)

for %%r in (!localfiles!) do (
    @REM generate file path
    call :pathconvert tfile %%r
    if "!tfile!" == "" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] é…ç½®æ— æ•ˆï¼Œè®¢é˜…æˆ–ä»£ç†è§„åˆ™æ›´æ–°å¤±è´¥
        goto :eof  
    )

    set "filepaths=!filepaths!,!tfile!"
    for /f "tokens=1* delims=," %%u in ("!texturls!") do (
        call :trim url %%u
        set "texturls=%%v"

        if /i "!url:~0,8!"=="https://" (
            @REM ghproxy
            call :ghproxywrapper url !url!

            set "needdownload=0"
            if not exist "!tfile!" set "needdownload=1"
            if "!force!" == "1" set "needdownload=1"
            @REM should download
            if "!needdownload!" == "1" (
                @REM get directory
                call :splitpath filepath filename "!tfile!"

                @REM mkdir if not exists
                call :makedirs success "!filepath!"

                @REM request and save
                curl.exe --retry 5 --retry-max-time 90 -m 120 --connect-timeout 15 -s -L -C - "!url!" > "!temp!\!filename!"

                @REM check file size
                set "filesize=0"
                if exist "!temp!\!filename!" (
                    for %%a in ("!temp!\!filename!") do set "filesize=%%~za"
                )

                @REM check file content
                call :verify match "!temp!\!filename!" "!check!"

                if !filesize! GTR 16 if "!match!" == "1" (
                    @REM delete if old file exists
                    del /f /q "!tfile!" >nul 2>nul

                    @REM move new file to dest
                    move "!temp!\!filename!" "!filepath!" >nul 2>nul

                    @REM changed status 
                    set "%~1=1"
                ) else (
                    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ–‡ä»¶ %ESC%[!warncolor!m!filename!%ESC%[0m ä¸‹è½½å¤±è´¥ï¼Œä¸‹è½½é“¾æ¥ï¼š"!url!"
                )
            )
        )
    )
)

set "%~5=!filepaths!"
@REM delete tempfile
if exist "!tempfile!" del /f /q "!tempfile!" >nul 2>nul
goto :eof


@REM extract dashboard path
:extractpath <result>
set "%~1="

if not exist "!configfile!" goto :eof

set "keyname="
set "content="
for /f "tokens=1,* delims=:" %%a in ('findstr /i /r /c:"external-ui:[ ][ ]*" "!configfile!"') do (
    set "keyname=%%a"
    set "content=%%b"
)

@REM not found 'external-ui' configuration in config file
call :trim keyname "!keyname!"
if "!keyname!" NEQ "external-ui" goto :eof

call :trim content "!content!"
if "!content!" == "" goto :eof

call :pathconvert directory "!content!"
set "%~1=!directory!"
goto :eof


@REM check file is validate
:verify <result> <file> <check>
set "%~1=0"
call :trim candidate "%~2"
if not exist "!candidate!" goto :eof

call :trim check "%~3"
if "!check!" == "" (
    set "%~1=1"
    goto :eof
)

set "text="
for /f "tokens=1* delims=:" %%a in ('findstr /i /r /c:"!check!:[ ]*" "!candidate!"') do set "text=%%a"

@REM not required
call :trim text "!text!"
if "!text!" == "!check!" set "%~1=1"
goto :eof


@REM upgrade dashboard
:dashboardupdate <force>
call :trim force "%~1"
if "!force!" == "" set "force=0"

if "!dashboardurl!" == "" (
    if "!force!" == "0" goto :eof

    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] æ§åˆ¶é¢æ¿%ESC%[!warncolor!mæœªå¯ç”¨%ESC%[0mï¼Œè·³è¿‡æ›´æ–°
    goto :eof
)

if "!dashboard!" == "" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ³•è·å–æ§åˆ¶é¢æ¿ä¿å­˜è·¯å¾„
    goto :eof
)

@REM exists
if exist "!dashboard!\index.html" if "!force!" == "0" goto :eof
call :makedirs success "!dashboard!"

@echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] å¼€å§‹ä¸‹è½½å¹¶æ›´æ–°æ§åˆ¶é¢æ¿
curl.exe --retry 5 -m 120 --connect-timeout 20 -s -L -C - -o "!temp!\dashboard.zip" "!dashboardurl!"

if not exist "!temp!\dashboard.zip" (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] æ§åˆ¶é¢æ¿ä¸‹è½½å¤±è´¥ï¼Œä¸‹è½½é“¾æ¥ï¼š"!dashboardurl!"
    goto :eof
)

@REM unzip
tar -xzf "!temp!\dashboard.zip" -C !temp! >nul 2>nul
del /f /q "!temp!\dashboard.zip" >nul 2>nul

@REM base path and directory name
call :splitpath dashpath dashname "!dashboard!"
if "!dashpath!" == "" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ³•è·å–æ§åˆ¶é¢æ¿ä¿å­˜è·¯å¾„
    goto :eof
)

if "!dashname!" == "" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ— æ³•è·å–æ§åˆ¶é¢æ¿æ–‡ä»¶å¤¹å
    goto :eof
)

@REM rename
ren "!temp!\!dashdirectory!" !dashname!

@REM replace if dashboard download success
dir /a /s /b "!temp!\!dashname!" | findstr . >nul && (
    call :replacedir "!temp!\!dashname!" "!dashboard!"
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] æ§åˆ¶é¢æ¿å·²æ›´æ–°è‡³æœ€æ–°ç‰ˆæœ¬
) || (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] æ§åˆ¶é¢æ¿ä¸‹è½½å¤±è´¥ï¼Œä¸‹è½½é“¾æ¥ï¼š"!dashboardurl!"
)
goto :eof


@REM overwrite files
:replacedir <src> <dest>
set "src=%~1"
set "target=%~2"

if "!src!" == "" (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] ç§»åŠ¨å¤±è´¥ï¼Œæºæ–‡ä»¶å¤¹è·¯å¾„ä¸ºç©º
    goto :eof
)

if "!target!" == "" (
    @echo [%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] ç§»åŠ¨å¤±è´¥ï¼Œç›®æ ‡è·¯å¾„ä¸ºç©º
    goto :eof
)

if not exist "!src!" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ–‡ä»¶å¤¹ç§»åŠ¨å¤±è´¥ï¼Œæºæ–‡ä»¶å¤¹ä¸å­˜åœ¨ï¼š"!src!"
    goto :eof  
)

@REM delete old folder if exists
if exist "!target!" rd "!target!" /s /q >nul 2>nul

@REM copy to dest
xcopy "!src!" "!target!" /h /e /y /q /i >nul 2>nul

@REM delete source dashboard
rd "!src!" /s /q >nul 2>nul
goto :eof


@REM delete if file exists
:cleanworkspace
set "directory=%~1"
if "!directory!" == "" set "directory=!temp!"

if exist "!directory!\clash.zip" del /f /q "!directory!\clash.zip" >nul
if exist "!directory!\clash.exe" del /f /q "!directory!\clash.exe" >nul

@REM wintun
if exist "!directory!\wintun.zip" del /f /q "!directory!\wintun.zip"
if exist "!directory!\wintun" rd "!directory!\wintun" /s /q >nul 2>nul

if "!clashexe!" NEQ "" (
    if exist "!directory!\!clashexe!" del /f /q "!directory!\!clashexe!" >nul
)

if "!countryfile!" NEQ "" (
    if exist "!directory!\!countryfile!" del /f /q "!directory!\!countryfile!" >nul
)

if "!geositefile!" NEQ "" (
    if exist "!directory!\!geositefile!" del /f /q "!directory!\!geositefile!" >nul
)

if "!geoipfile!" NEQ "" (
    if exist "!directory!\!geoipfile!" del /f /q "!directory!\!geoipfile!" >nul
)

@REM delete directory
if "!dashdirectory!" NEQ "" (
    if exist "!directory!\!dashdirectory!" rd "!directory!\!dashdirectory!" /s /q >nul
)

if "!dashboard!" == "" goto :eof
if exist "!directory!\!dashboard!.zip" del /f /q "!directory!\!dashboard!.zip" >nul
if exist "!directory!\!dashboard!" rd "!directory!\!dashboard!" /s /q >nul 2>nul
goto :eof


@REM replace '\\' to '\' for directory 
:pathregular <result> <directory>
set "%~1="
call :trim directory "%~2"

if "!directory!" == "" goto :eof

@REM '\\' to '\'
set "directory=!directory:\\=\!"

@REM '/' to '\'
set "directory=!directory:/=\!"

@REM remove last '\'
if "!directory:~-1!" == "\" set "directory=!directory:~0,-1!"
set "%~1=!directory!"
goto :eof


@REM define exit function
:terminate
@echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ›´æ–°å¤±è´¥ï¼Œä»£ç†ç¨‹åºã€åŸŸååŠ IP åœ°å€æ•°æ®åº“æˆ–æ§åˆ¶é¢æ¿ç¼ºå¤±
call :cleanworkspace "!temp!"
exit /b 1
goto :eof


@REM close
:closeproxy
call :isrunning status
if "!status!" == "0" (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] ç½‘ç»œä»£ç†ç¨‹åº%ESC%[!warncolor!mæœªè¿è¡Œ%ESC%[0mï¼Œæ— é¡»å…³é—­
    goto :eof
)

set "tips=[%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] æ­¤æ“ä½œå°†ä¼šå…³é—­ä»£ç†ç½‘ç»œï¼Œæ˜¯å¦ç»§ç»­ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
if "!msterminal!" == "1" (
    choice /t 6 /d y /n /m "!tips!"
) else (
    set /p "=!tips!" <nul
    choice /t 6 /d y /n
)
if !errorlevel! == 2 exit /b 1
goto :killprocesswrapper


@REM output with color
:setESC
for /F "tokens=1,2 delims=#" %%a in ('"prompt #$H#$E# & echo on & for %%b in (1) do rem"') do (
  set ESC=%%b
  exit /b 0
)
exit /b 0


@REM set proxy
:enableproxy <server>
call :trim server "%~1"
if "!server!" == "" goto :eof

reg add "!proxyregpath!" /v ProxyEnable /t REG_DWORD /d 1 /f >nul 2>nul
reg add "!proxyregpath!" /v ProxyServer /t REG_SZ /d "!server!" /f >nul 2>nul
reg add "!proxyregpath!" /v ProxyOverride /t REG_SZ /d "<local>" /f >nul 2>nul
goto :eof


@REM cancel proxy
:disableproxy
reg add "!proxyregpath!" /v ProxyServer /t REG_SZ /d "" /f >nul 2>nul
reg add "!proxyregpath!" /v ProxyEnable /t REG_DWORD /d 0 /f >nul 2>nul
reg add "!proxyregpath!" /v ProxyOverride /t REG_SZ /d "" /f >nul 2>nul
goto :eof


@REM query proxy status
:systemproxy <result>
set "%~1="

@REM enabled
call :regquery enable "!proxyregpath!" "ProxyEnable" "REG_DWORD"
if "!enable!" NEQ "0x1" goto :eof

@REM proxy server
call :regquery server "!proxyregpath!" "ProxyServer" "REG_SZ"
if "!server!" NEQ "" set "%~1=!server!"
goto :eof


@REM auto start when user login
:autostart
call :regquery exename "!autostartregpath!" "Clash" "REG_SZ"
if "!startupvbs!" NEQ "!exename!" (
    set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] æ˜¯å¦å…è®¸ç½‘ç»œä»£ç†ç¨‹åºå¼€æœºè‡ªå¯ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
    if "!msterminal!" == "1" (
        choice /t 5 /d y /n /m "!tips!"
    ) else (
        set /p "=!tips!" <nul
        choice /t 5 /d y /n
    )
    if !errorlevel! == 2 exit /b 1

    call :nopromptrunas success
    if "!success!" == "0" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æƒé™å—é™ï¼Œ%ESC%[91mæ— æ³•è®¾ç½®%ESC%[0må¼€æœºè‡ªå¯
        goto :eof
    )

    call :generatestartvbs "!startupvbs!" "-r"
    call :registerexe success "!startupvbs!"
    if "!success!" == "1" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] ç½‘ç»œä»£ç†ç¨‹åºå¼€æœºè‡ªå¯è®¾ç½®%ESC%[!infocolor!må®Œæˆ%ESC%[0m
    ) else (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] ç½‘ç»œä»£ç†ç¨‹åºå¼€æœºè‡ªå¯è®¾ç½®%ESC%[91må¤±è´¥%ESC%[0m
    )
)
goto :eof


@REM disable auto start
:disableautostart <result>
set "%~1=0"
call :regquery exename "!autostartregpath!" "Clash" "REG_SZ"

if "!exename!" == "" (
    set "%~1=1"
) else (
    set "shoulddelete=1"
    if "!startupvbs!" NEQ "!exename!" (
        set "tips=[%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] å‘ç°ç›¸åŒåå­—ä½†æ‰§è¡Œè·¯å¾„ä¸åŒçš„é…ç½®ï¼Œæ˜¯å¦ç»§ç»­ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
        if "!msterminal!" == "1" (
            choice /t 5 /d n /n /m "!tips!"
        ) else (
            set /p "=!tips!" <nul
            choice /t 5 /d n /n
        )
        if !errorlevel! == 2 set "shoulddelete=0"
    )
    if "!shoulddelete!" == "1" (
        reg delete "!autostartregpath!" /v "Clash" /f >nul 2>nul
        if "!errorlevel!" == "0" set "%~1=1"

        @REM disable
        reg delete "!startupapproved!" /v "Clash" /f >nul 2>nul
    )
)
goto :eof


@REM add scheduled tasks
:autoupdate <refresh>
call :trim refresh "%~1"
if "!refresh!" == "" set "refresh=0"
set "taskname=ClashUpdater"

call :taskstatus ready "!taskname!"
if "!refresh!" == "1" set "ready=0"

if "!ready!" == "0" (
    set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] æ˜¯å¦è®¾ç½®è‡ªåŠ¨æ£€æŸ¥æ›´æ–°ä»£ç†åº”ç”¨åŠè§„åˆ™ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
    if "!msterminal!" == "1" (
        choice /t 5 /d n /n /m "!tips!"
    ) else (
        set /p "=!tips!" <nul
        choice /t 5 /d y /n
    )
    if !errorlevel! == 2 exit /b 1

    set "operation=-u"
    if "!clashmeta!" == "1" set "operation=!operation! -m"
    if "!alpha!" == "1" set "operation=!operation! -a"
    if "!yacd!" == "1" set "operation=!operation! -y"

    call :generatestartvbs "!updatevbs!" "!operation!"
    call :deletetask success "!taskname!"
    call :createtask success "!updatevbs!" "!taskname!"
    if "!success!" == "1" (
        @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] è‡ªåŠ¨æ£€æŸ¥æ›´æ–°è®¾ç½®%ESC%[!infocolor!mæˆåŠŸ%ESC%[0m
    ) else (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] è‡ªåŠ¨æ£€æŸ¥æ›´æ–°è®¾ç½®%ESC%[91må¤±è´¥%ESC%[0m
    )
)
goto :eof


@REM create scheduled tasks
:createtask <result> <path> <taskname>
set "%~1=0"
call :trim exename "%~2"
if "!exename!" == "" goto :eof

call :trim taskname "%~3"
if "!taskname!" == "" goto :eof

@REM create
schtasks /create /tn "!taskname!" /tr "!exename!" /sc daily /mo 1 /ri 360 /st 09:15 /du 0012:00 /f >nul 2>nul
if "!errorlevel!" == "0" set "%~1=1"
goto :eof


@REM query scheduled tasks
:taskstatus <status> <taskname>
set "%~1=0"
call :trim taskname "%~2"
if "!taskname!" == "" goto :eof

@REM query
schtasks /query /tn "!taskname!" >nul 2>nul
if "!errorlevel!" NEQ "0" goto :eof

set "status="
for /f "usebackq skip=3 tokens=4" %%a in (`schtasks /query /tn "!taskname!"`) do set "status=%%a"
call :trim status "!status!"

if "!status!" == "Ready" set "%~1=1"
goto :eof


@REM delete update tasks
:deletetask <result> <taskname>
set "%~1=0"
call :trim taskname "%~2"
if "!taskname!" == "" goto :eof

schtasks /query /tn "!taskname!" >nul 2>nul
@REM not found
if "!errorlevel!" NEQ "0" (
    set "%~1=1"
    goto :eof
)

@REM remove
call :privilege "goto :cancelscheduled !taskname!" 0

@REM get delete status
for /l %%i in (1,1,5) do (
    schtasks /query /tn "!taskname!" >nul 2>nul
    if "!errorlevel!" == "0" (
        @REM wait
        timeout /t 1 /nobreak >nul 2>nul
    ) else (
        set "%~1=1"
        exit /b
    )
)
goto :eof


@REM remove scheduled task
:cancelscheduled <taskname>
@REM delete
schtasks /delete /tn "%~1" /f  >nul 2>nul

@REM get administrator privileges
call :nopromptrunas result
goto :eof


@REM add to 
:registerexe <result> <path>
set "%~1=0"
call :trim exename "%~2"
if "!exename!" == "" goto :eof
if not exist "!exename!" goto :eof

@REM delete
reg delete "!autostartregpath!" /v "Clash" /f >nul 2>nul
@REM register
reg add "!autostartregpath!" /v "Clash" /t "REG_SZ" /d "!exename!" >nul 2>nul
if "!errorlevel!" NEQ "0" goto :eof

@REM approved
reg delete "!startupapproved!" /v "Clash" /f >nul 2>nul
@REM register
reg add "!startupapproved!" /v "Clash" /t "REG_BINARY" /d "02 00 00 00 00 00 00 00 00 00 00 00" >nul 2>nul

if "!errorlevel!" == "0" set "%~1=1"
goto :eof


@REM vbs for startup
:generatestartvbs <path> <operation>
call :trim startscript "%~1"
if "!startscript!" == "" goto :eof

call :trim operation "%~2"
if "!operation!" == "" goto :eof

@echo set ws = WScript.CreateObject^("WScript.Shell"^) > "!startscript!"
@echo ws.Run "%~dp0!batname! !operation! -w !dest! -c !configfile!", 0 >> "!startscript!"
@echo set ws = Nothing >> "!startscript!"
goto :eof


@REM judge os caption
:ishomeedition <result>
set "%~1=1"

set "content=" 
for /f %%a in ('wmic os get OperatingSystemSKU ^| findstr /r /i /c:"^[1-9][0-9]*"') do set "content=%%a"
call :trim content "!content!"

@REM 2/3/5/26 represent home edition
if "!content!" NEQ "2" if "!content!" NEQ "3" if "!content!" NEQ "5" if "!content!" NEQ "26" (
    for /f "delims=" %%a in ('wmic os get caption ^| findstr /i /c:"pro" /c:"professional"') do set "content=%%a"
    call :trim content "!content!"
    if "!content!" NEQ "" set "%~1=0"
)
goto :eof


@REM enable run as admin
:enablerunas <result>
set "%~1=1"

call :ishomeedition edition
if "!edition!" == "0" goto :eof

set "packagesfile=!temp!\grouppolicypackages.txt"

@REM find all grouppolicy pakcages
dir /b "C:\Windows\servicing\Packages\Microsoft-Windows-GroupPolicy-ClientExtensions-Package~3*.mum" > "!packagesfile!"
dir /b "C:\Windows\servicing\Packages\Microsoft-Windows-GroupPolicy-ClientTools-Package~3*.mum" >> "!packagesfile!"

@REM install
for /f %%i in ('findstr /i . "!packagesfile!" 2^>nul') do dism /online /norestart /add-package:"C:\Windows\servicing\Packages\%%i" >nul 2>nul
if "!errorlevel!" NEQ "0" set "%~1=0"

del /f /q "!packagesfile!" >nul 2>nul
goto :eof


@REM no prompt when run as admin
:nopromptrunas <result>
set "%~1=0"

@REM regedit path and key
set "grouppolicy=HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System"
set "gprkey=ConsentPromptBehaviorAdmin"

call :regquery code "!grouppolicy!" "!gprkey!" "REG_DWORD"
if "!code!" == "0x0" (
    set "%~1=1"
    exit /b  
)

call :enablerunas enable
if "!enable!" == "0" goto :eof

@REM change regedit
reg delete "!grouppolicy!" /v ConsentPromptBehaviorAdmin /f >nul 2>nul
reg add "!grouppolicy!" /v ConsentPromptBehaviorAdmin /t REG_DWORD /d 0 /f >nul 2>nul
if "!errorlevel!" == "0" set "%~1=1"
goto :eof


@REM clean data
:purge
set "tips=[%ESC%[!warncolor!mè­¦å‘Š%ESC%[0m] å³å°†å…³é—­ç³»ç»Ÿä»£ç†å¹¶ç¦ç”¨å¼€æœºè‡ªå¯ï¼Œæ˜¯å¦ç»§ç»­ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
if "!msterminal!" == "1" (
    choice /t 6 /d n /n /m "!tips!"
) else (
    set /p "=!tips!" <nul
    choice /t 6 /d n /n
)
if !errorlevel! == 2 exit /b 1

@REM close system proxy
call :disableproxy

@REM disable auto start
call :disableautostart success
if "!success!" == "0" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] å¼€æœºè‡ªå¯%ESC%[91mç¦ç”¨å¤±è´¥%ESC%[0mï¼Œå¯åœ¨%ESC%[!warncolor!mä»»åŠ¡ç®¡ç†ä¸­å¿ƒ%ESC%[0mæ‰‹åŠ¨è®¾ç½®
)

@REM delete scheduled
call :deletetask success "ClashUpdater"
if "!success!" == "0" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] è‡ªåŠ¨æ£€æŸ¥è·Ÿæ–°å–æ¶ˆ%ESC%[91må¤±è´¥%ESC%[0mï¼Œå¯åœ¨%ESC%[!warncolor!mä»»åŠ¡è®¡åˆ’ç¨‹åº%ESC%[0mä¸­æ‰‹åŠ¨åˆ é™¤ 
)

@REM stop process
call :killprocesswrapper

@REM remote shortcut
call :deleteshortcut

@echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] æ¸…ç†%ESC%[!infocolor!må®Œæ¯•%ESC%[0m, bye~
goto :eof


@REM query value form register
:regquery <result> <path> <key> <type>
set "%~1="
set "value="

@REM path
call :trim rpath "%~2"
if "!rpath!" == "" goto :eof

@REM key
call :trim rkey "%~3"
if "!rkey!" == "" goto :eof

@REM type
call :trim rtype "%~4"
if "!rtype!" == "" set "rtype=REG_SZ"

@REM query
reg query "!rpath!" /V "!rkey!" >nul 2>nul
if "!errorlevel!" NEQ "0" goto :eof

for /f "tokens=3" %%a in ('reg query "!rpath!" /V "!rkey!" ^| findstr /r /i "!rtype!"') do set "value=%%a"
call :trim value "!value!"
set "%~1=!value!"
goto :eof


@REM icon generation
:downloadicon <result> <iconname>
set "%~1=0"

call :trim iconname "%~2"
if "!iconname!" == "" goto :eof

call :ghproxywrapper iconurl "https://raw.githubusercontent.com/wzdnzd/aggregator/master/clash.ico"
set "statuscode=000"
for /f %%a in ('curl --retry 3 --retry-max-time 60 -m 60 --connect-timeout 30 -L -s -o "!dest!\!iconname!" -w "%%{http_code}" "!iconurl!"') do set "statuscode=%%a"

if "!statuscode!" == "200" set "%~1=1"
goto :eof


@REM create desktop shortcut
:createshortcut <result> <linkdest> <target> <iconname>
set "%~1=0"
call :trim linkdest "%~2"
call :trim target "%~3"
call :trim iconname "%~4"


if "!linkdest!" == "" goto :eof
if "!target!" == "" goto :eof
if "!iconname!" == "" set "iconname=clash.ico"
if exist "!linkdest!" del /f /q "!linkdest!" >nul

set "vbspath=!temp!\createshortcut.vbs"
((
    @echo set ows = WScript.CreateObject^("WScript.Shell"^) 
    @echo slinkfile = ows.ExpandEnvironmentStrings^("!linkdest!"^)
    @echo set olink = ows.CreateShortcut^(slinkfile^) 
    @echo olink.TargetPath = ows.ExpandEnvironmentStrings^("!target!"^)
    @echo olink.IconLocation = ows.ExpandEnvironmentStrings^("!dest!\!iconname!"^)
    @echo olink.WorkingDirectory = ows.ExpandEnvironmentStrings^("!dest!"^)
    @echo olink.Save
) 1>!vbspath!

cscript //nologo "!vbspath!"
if "!errorlevel!" == "0" set "%~1=1"

del /f /q "!vbspath!"
) >nul
goto :eof


@REM send to desktop
:adddesktop
if "!enableshortcut!" == "0" goto :eof

set "iconname=clash.ico"
set "linkdest=!HOMEDRIVE!!HOMEPATH!\Desktop\Clash.lnk"

set "exepath="
@REM parse target if link exists
if exist "!linkdest!" (
    for /f "delims=" %%a in ('wmic path win32_shortcutfile where "name='!linkdest:\=\\!'" get target /value') do (
        for /f "tokens=2 delims==" %%b in ("%%~a") do set "exepath=%%b"
    )
)

call :trim exepath "!exepath!"
if "!exepath!" == "!startupvbs!" goto :eof

set "tips=[%ESC%[!warncolor!mæç¤º%ESC%[0m] æ˜¯å¦æ·»åŠ æ¡Œé¢å¿«æ·æ–¹å¼ï¼Ÿ(%ESC%[!warncolor!mY%ESC%[0m/%ESC%[!warncolor!mN%ESC%[0m) "
if "!msterminal!" == "1" (
    choice /t 5 /d y /n /m "!tips!"
) else (
    set /p "=!tips!" <nul
    choice /t 5 /d y /n
)
if !errorlevel! == 2 goto :eof

if not exist "!dest!\!iconname!" (
    call :downloadicon finished "!iconname!"
    if "!finished!" == "0" (
        @echo [%ESC%[91mé”™è¯¯%ESC%[0m] åº”ç”¨å›¾æ ‡æ–‡ä»¶ä¸‹è½½%ESC%[91må¤±è´¥%ESC%[0mï¼Œæ— æ³•åˆ›å»ºæ¡Œé¢å¿«æ·æ–¹å¼
        goto :eof
    )
)

call :createshortcut finished "!linkdest!" "!startupvbs!" "!iconname!"
if "!finished!" == "0" (
    @echo [%ESC%[91mé”™è¯¯%ESC%[0m] æ¡Œé¢å¿«æ·æ–¹å¼æ·»åŠ %ESC%[91må¤±è´¥%ESC%[0mï¼Œå¦‚æœ‰éœ€è¦ï¼Œè¯·è‡ªè¡Œåˆ›å»º
) else (
    @echo [%ESC%[!infocolor!mä¿¡æ¯%ESC%[0m] æ¡Œé¢å¿«æ·æ–¹å¼æ·»åŠ %ESC%[!infocolor!mæˆåŠŸ%ESC%[0m
)
goto :eof


@REM remove shortcut from desktop
:deleteshortcut
set "linkpath=!HOMEDRIVE!!HOMEPATH!\Desktop\Clash.lnk"
del /f /q "!linkpath!" >nul 2>nul
goto :eof


@REM determine whether it is a microsoft terminal
:ismsterminal <result>
set "%~1=0"

call :whatterminal output 3
call :trim output "!output!"

set "retry=0"
if /i "!output!" == "powershell" set "retry=1"
if /i "!output!" == "pwsh" set "retry=1"

if "!retry!" == "1" (
    call :whatterminal output 4
    call :trim output "!output!"
)

if /i "!output!" == "WindowsTerminal" (
    set "%~1=1"
    goto :eof
)
goto :eof


@REM get current terminal name
:whatterminal <result> <num>
set "%~1="
call :trim num "%~2"
if "!num!" == "" set "num=3"

@REM set "pscmd=$current = Get-CimInstance -ClassName win32_process -filter ('ProcessID='+$pid); $parent = Get-Process -id ($current.parentprocessID); if ($parent.ProcessName -eq 'WindowsTerminal') {echo 'true';} else {$cimgrandparent = Get-CimInstance -ClassName win32_process -filter ('Processid='+($($parent.id))); $grandparent = Get-Process -id ($cimgrandparent.parentProcessId); if (($grandparent.processname) -eq 'WindowsTerminal') {echo 'true';} else {echo 'false';}}"

@REM reference: https://stackoverflow.com/questions/53447286/in-a-cmd-batch-file-can-i-determine-if-it-was-run-from-powershell
set "pscmd=$ppid=$pid;while($i++ -lt !num! -and ($ppid=(Get-CimInstance Win32_Process -Filter ('ProcessID='+$ppid)).ParentProcessId)) {}; (Get-Process -EA Ignore -ID $ppid).Name"

for /f "tokens=*" %%a in ('powershell -noprofile -command "!pscmd!"') do set "%~1=%%a"
goto :eof


endlocal
